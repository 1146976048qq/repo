# Maintainer: Dominik Mayer <dominik.mayer@gmail.com>
# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgname=google-talkplugin
pkgver=5.40.2.0
pkgrel=1
pkgdesc="Video chat browser plug-in for Google Talk"
arch=('i686' 'x86_64')
url="http://www.google.com/chat/video"
license=('custom:google')
depends=('gtk2>=2.12.0' 'alsa-lib' 'mesa' 'lsb-release')
# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')
optdepends=('libnotify' 'pulseaudio')
source=(license.html::https://www.google.com/intl/en/policies/terms/index.html)
sha1sums=('b48ddfeeac65b84278040973bea589809cffe5d3')
sha1sums_i686=('9ffe0b6aabf45f0e8a5f94f00909005f134f7c2d')
sha1sums_x86_64=('1fde513103c24c339547ffdacd998d11a89e0a0a')
source_i686+=("https://dl.google.com/linux/talkplugin/rpm/stable/i386/${pkgname}-${pkgver}-1.i386.rpm")
source_x86_64+=("https://dl.google.com/linux/talkplugin/rpm/stable/x86_64/${pkgname}-${pkgver}-1.x86_64.rpm")

package() {
	cp -R "${srcdir}"/opt "${pkgdir}"
	install -d "${pkgdir}"/usr/lib
	cp -R "${srcdir}"/usr/lib*/* "${pkgdir}"/usr/lib
    
	# makeing it compatible with chromium and chromium-dev for Arch
	cp -R "${pkgdir}"/usr/lib/chromium-browser "${pkgdir}"/usr/lib/chromium
	cp -R "${pkgdir}"/usr/lib/chromium-browser "${pkgdir}"/usr/lib/chromium-dev
    
	# cleaning up cron update script
	rm -rf "${pkgdir}"/opt/google/talkplugin/cron
    
	# install license file
	install -Dm644 "${srcdir}"/license.html "${pkgdir}"/usr/share/licenses/${pkgname}/license.html
}
