# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-nose
pkgname=(${_pypy3name}-nose ${_pypyname}-nose ${_pypyname}-nose-doc)
pkgver=1.3.4
pkgrel=6
pkgdesc="A discovery-based unittest extension"
arch=('any')
url='http://readthedocs.org/docs/nose/'
license=('LGPL2.1')
makedepends=(pypy pypy3-${_pypy3abi} pypy-sphinx pypy-setuptools)
source=("http://pypi.python.org/packages/source/n/nose/nose-${pkgver}.tar.gz")
md5sums=('6ed7169887580ddc9a8e16048d38274d')

build() {
  sed -i -e "s:man/man1:share/man/man1:g" nose-$pkgver/setup.py
  cp -R nose{,2}-$pkgver

  (cd nose-$pkgver
   pypy3 setup.py build)

  (cd nose2-$pkgver
   pypy setup.py build)
}

package_pypy3-hg-nose() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-nose=${pkgver}
            pypy3-nose=${pkgver})
  conflicts=(pypy3-nose)
  cd nose-$pkgver

  pypy3 setup.py install --root="${pkgdir}" -O1 --skip-build

  install -dm755 "${pkgdir}/usr"
  mv "${pkgdir}/opt/pypy3/bin" "${pkgdir}/usr"
  mv "$pkgdir/usr/bin/nosetests" "$pkgdir/usr/bin/nosetests-pypy3"
  mv "$pkgdir/usr/bin/nosetests-3.2" "$pkgdir/usr/bin/nosetests-pypy3.2"
}

package_pypy-hg-nose() {
  depends=(pypy)
  provides=(pypy-nose=${pkgver})
  conflicts=(pypy-nose)
  cd nose2-$pkgver

  pypy setup.py install --root="${pkgdir}" -O1 --skip-build

  install -dm755 "${pkgdir}/usr"
  mv "${pkgdir}/opt/pypy/bin" "${pkgdir}/usr"
  mv "$pkgdir/usr/bin/nosetests" "$pkgdir/usr/bin/nosetests-pypy"
  mv "$pkgdir/usr/bin/nosetests-2.7" "$pkgdir/usr/bin/nosetests-pypy2.7"
  rm -rf "$pkgdir/usr/share"
}

package_pypy-hg-nose-doc(){
  pkgdesc="Nose documentation and examples"
  provides=(python-nose-doc)
  conflicts=(python-nose-doc)
  cd nose2-$pkgver/doc

  make SPHINXBUILD=sphinx-build-pypy html
  mkdir -p "$pkgdir/usr/share/doc/python-nose"
  cp -r .build/html "$pkgdir/usr/share/doc/python-nose"
  cp -r ../examples "$pkgdir/usr/share/doc/python-nose"
}
