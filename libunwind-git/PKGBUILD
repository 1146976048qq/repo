n# Maintainer: SÃ©bastien Luttringer
# Contributor: Lawrence Lee <valheru@facticius.net>
# Contributor: Phillip Marvin <phillip.marvin@gmail.com>
# Contributor: keystone <phillip.marvin@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build v5 with system libatomic_ops

pkgname=libunwind-git
pkgver=1.2.rc1.125.gab39ae9
pkgrel=3
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
depends=('glibc' 'xz')
makedepends=('git')
source=('git://git.savannah.gnu.org/libunwind.git'
        0001-Handle-non-signal-frame-unwind-info-lookup-in-ARM-ex.patch
        0002-Allow-DWARF-version-both-3-and-4.patch
        0003-Prefer-EXTBL-unwinding-on-ARM.patch
        0001-Use-ucontext_t-instead-of-struct-ucontext.patch
        0002-Fix-init-local-signal-test.patch
        0003-Fix-intermittent-test-failure-in-test-resume-sig.patch
        0004-Ignore-tests-Ltest-mem-validate.patch
        0005-dwarf-Fix-incorrect-cfi-execution.patch)
md5sums=('SKIP'
         '0021c63be85b8093210db2bf86872573'
         'ccf5ec5a8482086c7b8c2e4bc9cf35f4'
         '148852d93dc2e2a85ac167de707353f3'
         '155763301a49514c7f2fc3fbb7bf6333'
         '1cfa349e0cf07ffd7ea596851642b752'
         '59822a0908b1483fe7ef6040cf046b71'
         '03b2348091a2da49dcfa5cf58938bff6'
         '96418814c394e837daadd9dd211f575d')
options=('debug' '!strip')
provides=(libunwind)
conflicts=(libunwind)

pkgver() {
  cd libunwind

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd libunwind

  patch -Np1 < ../0001-Handle-non-signal-frame-unwind-info-lookup-in-ARM-ex.patch
  patch -Np1 < ../0002-Allow-DWARF-version-both-3-and-4.patch
  patch -Np1 < ../0003-Prefer-EXTBL-unwinding-on-ARM.patch
  patch -Np1 < ../0001-Use-ucontext_t-instead-of-struct-ucontext.patch
  patch -Np1 < ../0002-Fix-init-local-signal-test.patch
  patch -Np1 < ../0003-Fix-intermittent-test-failure-in-test-resume-sig.patch
  patch -Np1 < ../0004-Ignore-tests-Ltest-mem-validate.patch
  patch -Np1 < ../0005-dwarf-Fix-incorrect-cfi-execution.patch
}

build() {
  cd libunwind

  autoreconf -fi
  [[ $CARCH == "arm" ]] && LDFLAGS+=" -latomic_ops" && CFLAGS+=" -DAO_USE_PTHREAD_DEFS=1"

  ./configure CFLAGS="$CFLAGS -U_FORTIFY_SOURCE" --prefix=/usr
  make
}

# check() {
#   cd libunwind

#   # This function is ``supposed'' to fail. Upstream know, but haven't fixed it.
#   make check || return 0
# }

package() {
  cd libunwind

  make DESTDIR="$pkgdir/" install
  cat >> "$pkgdir/usr/include/libunwind.h" <<EOF
#ifndef JL_UNW_HAS_FORMAT_IP
#define JL_UNW_HAS_FORMAT_IP 1
#endif
EOF
}
