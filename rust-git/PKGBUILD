# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
_gitname=rust
pkgname=rust-git
pkgver=1.0.0.alpha.1437.g88d8ba5
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=(gcc shared-mime-info)
makedepends=(libffi perl python2 curl git)
# editor support has been removed, see
# 017b3a543182006fa64bdc8c721969ae38c125f9
source=('git://github.com/rust-lang/rust.git'
        'git://github.com/rust-lang/zsh-config')
md5sums=(SKIP SKIP)
provides=(rust)
conflicts=(rust)
options=(staticlibs)

pkgver() {
  cd $_gitname
  git describe | sed s/-/./g
}

build() {
  cd rust

  ./configure --prefix=/usr --disable-debug --disable-docs --disable-verify-install

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

#check() {
  #cd rust
  #make check
#}

package() {
  cd rust
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/vim" "$pkgdir/usr/share/licenses/$pkgname"

  install -Dm644 "$srcdir"/zsh-config/_rust "$pkgdir/usr/share/zsh/site-functions/_rust"

  install -m644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname"

  cd "$pkgdir/usr"

  cd lib
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
}
