# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Lawliet <yanzilme@gmail.com>

pkgname=nvidia-uksm
pkgver=319.17
_extramodules=extramodules-3.9-uksm
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=2
_pkgdesc="NVIDIA drivers for linux-uksm"
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-uksm>=3.9' 'linux-uksm<3.10' "nvidia-utils=${pkgver}")
makedepends=('linux-uksm-headers>=3.9' 'linux-uksm-headers<3.10')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck')
license=('custom')
options=(!strip)
install=nvidia-uksm.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('993eee683aea53b7965a853ccde3d740')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('4dda9b3e5c24136bebfaad122715c202')
fi

build() {
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-uksm.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-uksm.install"
	gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
