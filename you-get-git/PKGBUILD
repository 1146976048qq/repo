# Maintainer: lilydjwg <lilydjwg@gmail.com>
pkgname=you-get-git
pkgdesc="A YouTube/Youku/Sohu/Tudou/QQ/Sina/PPTV/Xiami/YouTube/Vimeo/ifeng/AcFun/bilibili/CNTV/... video downloader written in Python 3."
pkgver=0.3.30dev.20140716
pkgrel=1
arch=('any')
url="http://www.soimort.org/you-get/"
license=('MIT')
depends=('python' 'python-distribute')
makedepends=('git')
conflicts=(python-you-get-git python-you-get)
replaces=(python-you-get-git)
source=("git://github.com/soimort/you-get.git#branch=develop")
md5sums=(SKIP)

_repo_name=you-get

pkgver() {
  cd "$srcdir/$_repo_name"
  python3 -c "exec(compile(open('src/you_get/version.py').read(), 'version.py', 'exec')); print(__version__)" | tr '-' '.'
}

build() {
  cd "$srcdir/$_repo_name"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  python3 setup.py build
}

package() {
  cd "$srcdir/$_repo_name"
  python3 setup.py install --root=$pkgdir/ --optimize=1
}
