# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkipi-frameworks-git
pkgver=r1096.78105f8
pkgrel=1
pkgdesc="A interface to use kipi-plugins from a KDE image management program like digiKam. KF5 Frameworks branch (GIT version)"
url='http://www.digikam.org/sharedlibs'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kio' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
source=("git://anongit.kde.org/libkipi#branch=frameworks")
sha1sums=('SKIP')
install="libkipi-frameworks-git.install"

pkgver() {
  cd libkipi
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  sed 's|"${CMAKE_CURRENT_BINARY_DIR}/libkipi_version.h"|"${CMAKE_BINARY_DIR}/libkipi_version.h"|' -i libkipi/src/CMakeLists.txt
}

build() {
  cd build
  cmake ../libkipi \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

