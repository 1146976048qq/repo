# Maintainer: wicastC <wicastchen at hotmail.com>

pkgname=flamegraph-git
pkgver=r239.2287fa5
pkgrel=1.2
pkgdesc="stack trace visualizer"
arch=('any')
url="http://www.brendangregg.com/FlameGraphs/cpuflamegraphs.html"
license=('CDDL')
depends=('perl' 'awk')
makedepends=('git')
provides=('flamegraph')
conflicts=('flamegraph')
source=(${pkgname}::"git+https://github.com/brendangregg/FlameGraph")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}


package() {
  cd "$srcdir/$pkgname"

  for script in $(find -maxdepth 1 -name "*.pl" -o -name "*.awk");do
    install -Dm755 $script ${pkgdir}/usr/bin/$script
    ln -s ${script} ${pkgdir}/usr/bin/${script%.*}
  done;
  rm ${pkgdir}/usr/bin/files

  install -Dm644 docs/cddl1.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
