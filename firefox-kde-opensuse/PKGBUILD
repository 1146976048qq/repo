# Contributor: Weng Xuetian <wengxt@gmail.com>
# Maintainer: Yegorius <yegorius@domic.us>

# enable this if you run out of memory during linking
#_lowmem=true

pkgname=firefox-kde-opensuse
pkgver=23.0
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="https://www.mozilla.org/firefox/"
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'libvpx' 'libevent' 'nss' 'hunspell' 'sqlite'
         'libnotify' 'desktop-file-utils' 'hicolor-icon-theme'
         'kmozillahelper')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake'
             'xorg-server-xvfb' 'libpulse')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libpulse: PulseAudio audio driver')
provides=("firefox=${pkgver}")
conflicts=('firefox')
install=firefox.install
options=(!emptydirs)
_patchurl=https://build.opensuse.org/package/rawsourcefile/mozilla:Factory/MozillaFirefox
_patchrev=cdf792b65a71d76e6cb48d838870b3ec
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop firefox-install-dir.patch vendor.js shared-libs.patch
        cairo-shared.patch kde.js
	firefox-kde.patch::$_patchurl/firefox-kde.patch?srcmd5=$_patchrev
	mozilla-kde.patch::$_patchurl/mozilla-kde.patch?srcmd5=$_patchrev
	mozilla-nongnome-proxies.patch::$_patchurl/mozilla-nongnome-proxies.patch?srcmd5=$_patchrev)

prepare() {
  cd mozilla-release

  cp "$srcdir/mozconfig" .mozconfig

  patch -Np1 -i "$srcdir/firefox-install-dir.patch"
  patch -Np1 -i "$srcdir/shared-libs.patch"

  # Fix build with system cairo
  # https://bugzilla.mozilla.org/show_bug.cgi?id=722975
  patch -Np1 -i "$srcdir/cairo-shared.patch"

  msg "Patching for KDE"
  rm -f content/media/gstreamer/nsGStreamerFormatHelper.{h,cpp}
  rm -f browser/components/shell/src/{nsKDEShellService,nsUnixShellService}.{h,cpp}
  rm -f browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.{h,cpp}
  rm -f uriloader/exthandler/unix/{nsCommonRegistry,nsKDERegistry}.{h,cpp}
  rm -f toolkit/content/widgets/{dialog,preferences}-kde.xml

  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' browser/base/Makefile.in

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

  # WebRTC build tries to execute "python" and expects Python 2
  mkdir -p "$srcdir/path"
  ln -s /usr/bin/python2 "$srcdir/path/python"
}

build() {
  cd mozilla-release

  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"

  if [[ -n $_lowmem ]]; then
    LDFLAGS+=" -Wl,--no-keep-memory"
  fi

  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  make -f client.mk build
}

package() {
  cd mozilla-release
  make -f client.mk DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/vendor.js" "$pkgdir/usr/lib/firefox/browser/defaults/preferences/vendor.js"
  install -Dm644 "$srcdir/kde.js" "$pkgdir/usr/lib/firefox/browser/defaults/preferences/kde.js"

  for i in 16 22 24 32 48 256; do
      install -Dm644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done

  install -Dm644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -Dm644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/210x210/apps/firefox.png"

  install -Dm644 "$srcdir/firefox.desktop" "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}

md5sums=('794e0139c4df0392146353c655d94bb9'
         '9cf611028e87c9362e8d3914880c5601'
         '1df7fd9afd75d06decb129518a769133'
         '150ac0fb3ac7b2114c8e8851a9e0516c'
         '0d053487907de4376d67d8f499c5502b'
         '52e52f840a49eb1d14be1c0065b03a93'
         '3a236a2a39a60d622cf928655e68657f'
         '05bb69d25fb3572c618e3adf1ee7b670'
         'b251189ff372f3b0c86275f8dac2d9e1'
         '204ab5c4e4e00c840e3b3571f097f02b'
         '28f61de31540e20522aefdbe88a83fa8')
