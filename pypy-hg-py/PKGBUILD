# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-py
pkgname=(${_pypy3name}-py ${_pypyname}-py)
pkgver=1.4.26
pkgrel=2
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
makedepends=(pypy-setuptools pypy3-${_pypy3abi}-setuptools pypy3-${_pypy3abi})
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")
sha512sums=('5772893d754e4f3fee5de9fdb0eca2c654ac5288eba5c89dac88fd4d9acb2c0795bae61e0c43b37a14471536a1649c9138ac1c4374cd5697307e74062e9813f3')

prepare() {
  cp -a py-${pkgver}{,-py2}
}

build() {
  (cd py-${pkgver}
   pypy3 setup.py build)

  (cd py-${pkgver}-py2
   pypy setup.py build)
}

package_pypy3-hg-py() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-py=${pkgver}
            pypy3-py=${pkgver})
  conflicts=(pypy3-py)
  cd py-${pkgver}

  pypy3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_pypy-hg-py() {
  depends=(pypy)
  provides=(pypy-py=${pkgver})
  conflicts=(pypy-py)
  cd py-${pkgver}-py2

  pypy setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
