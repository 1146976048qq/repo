# Original author: Sven-Hendrik Haase (svenstaro) <sh@lutzhaase.com>
# Maintainer: Zack Buhman (buhman) <zack@buhman.org>
# Contributor: William Giokas (KaiSforza) <1007380@gmail.com>

USE_PYPY=1
USE_SHARED=1

pkgname=pypy-hg
pkgver=74241.3d8fe96dc4d9
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://codespeak.net/pypy/"
arch=('i686' 'x86_64')
depends=('libffi' 'openssl' 'expat' 'ncurses' 'zlib' 'bzip2')
options=('debug' 'strip' '!emptydirs')
# Force cpython2 to be installed for mercurial
makedepends=('python2>=2.7' 'mercurial' 'gdbm' 'sqlite' 'tcl' 'tk')
if ((USE_PYPY)); then
    makedepends+=('pypy')
fi
conflicts=('pypy')
provides=('pypy=2.5')
optdepends=('sqlite: sqlite3 module'
            'gdbm: gdbm module'
            'tk: lib-tk modules'
            'tcl: lib-tk modules')
license=('custom:MIT')

source=('hg+https://bitbucket.org/pypy/pypy')
md5sums=('SKIP')

_check_python() {
  _PYTHON=python2
  if ((USE_PYPY)) && which pypy &> /dev/null; then
      _PYTHON=pypy
  fi
}

pkgver() {
  cd "${srcdir}"/pypy
  echo "$(hg id -n).$(hg id -i)"
}

build() {
  cd "${srcdir}/pypy/pypy/goal"

  _check_python

  translate_options=()

  if ((USE_SHARED)); then
      translate_options+=('--shared')
  fi

  $_PYTHON ../../rpython/bin/rpython -Ojit "${translate_options[@]}" \
    targetpypystandalone
}

package() {
  cd "${srcdir}"/pypy/pypy/tool/release

  _check_python

  LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:../../goal/" $_PYTHON package.py \
    ../../../ pypy pypy-c "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  if ((USE_SHARED)); then
      mkdir -p "${pkgdir}"/opt/pypy/lib
      cp ../../goal/libpypy-c.so "${pkgdir}"/opt/pypy/lib/
      mkdir -p "${pkgdir}"/usr/lib
      ln -s /opt/pypy/lib/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  fi

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy-c "${pkgdir}"/usr/bin/pypy

  install -Dm644 "${pkgdir}"/opt/pypy/LICENSE \
    "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
