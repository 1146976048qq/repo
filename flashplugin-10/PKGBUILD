# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Xavion <Xavion0@gmail.com>
# Contributor: Michael Witten <mfwitten>

_langname=flash
_appname=${_langname}player
_basename=${_langname}plugin
pkgname=$_basename-10
pkgver=10.3.183.90
pkgrel=1
pkgdesc="Adobe Flash Player v10 (32-bit) - with GPU hardware acceleration"
arch=(i686 x86_64)
url=http://www.adobe.com/products/flashruntimes.html
license=(custom)
depends=(hicolor-icon-theme lib32-alsa-lib lib32-curl lib32-gtk2 lib32-libxt lib32-nss mozilla-common)
optdepends=('lib32-libvdpau: video hardware decoding for supporting players'
    'nspluginwrapper: make this plugin work with 64-bit browsers'
    'firefox32: 32-bit Firefox for 64-bit systems')
if [[ $CARCH == "i686" ]]; then
    depends=(${depends[@]/lib32-/})
    optdepends=('libvdpau: video hardware decoding for supporting players')
fi
provides=($_basename=$pkgver)
conflicts=($_basename)
options=(!strip)
install=$pkgname.install
backup=(etc/adobe/mms.cfg)
source=($pkgname-$pkgver.tar.gz::http://fpdownload.macromedia.com/get/flashplayer/current/licensing/linux/install_flash_player_10_linux.tar.gz
    mms.cfg)
sha256sums=('b8b6496bcccbea57026b1926157436bc167dfaa8bf9d4a3c51f308f90255d4bc'
    '27dad99feecfd7152305bd86e8149e8c6482b7dc787ccd205c05d1dc13aafead')
sha512sums=('b05dafa959ccf3d1291d769c4b60f00d75ea81a29e8162185eefdcafb09cf4392f5112e2ef0263d29a83c2c68571d32fb9af35cf548e6ccc79373cb6b1503d6f'
    'a5e273179d026c9f0c8609a8076cc5c129faf21dd605899b7904e53c2bdf899de94c7e258c2b0ac381a38b68675bdd381a47d1bfbf8ebcd6f10e6a4c8ac9207a')

package() {
    install -Dm644 mms.cfg "$pkgdir"/etc/adobe/mms.cfg
    mv usr/bin/flash-player-properties{,-10}
    cp -r usr/ "$pkgdir"/usr
    install -Dm755 lib$_appname.so "$pkgdir"/usr/lib/mozilla/plugins/lib$_appname.so
}
