# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy
# _pypyabi=-24
_pypy3name=pypy3
# _pypy3abi=-24

pkgbase=${_pypyname}-setuptools
pkgname=(${_pypy3name}-setuptools ${_pypyname}-setuptools)
pkgver=12.0.3
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
source=(http://pypi.python.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz)
md5sums=('f07e4b0f4c1c9368fcd980d888b29a65')
makedepends=(pypy pypy3${_pypy3abi})

prepare() {
  cp -r setuptools-${pkgver} pypy3-setuptools-${pkgver}

  (cd setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env pypy|" \
       setuptools/command/easy_install.py)

  (cd pypy3-setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env pypy3|" \
       setuptools/command/easy_install.py)
}

build() {
  (cd pypy3-setuptools-${pkgver}
   pypy3 setup.py build)

  (cd setuptools-${pkgver}
   pypy setup.py build)
}

package_pypy-setuptools() {
  depends=(pypy)
  # provides=(pypy${_pypyabi}-setuptools=${pkgver}-${pkgrel})
  cd setuptools-${pkgver}
  pypy setup.py install \
       --root="${pkgdir}" --optimize=1 --skip-build

  cd "${pkgdir}"
  mkdir -p usr/bin
  ln -s ../../opt/pypy/bin/easy_install usr/bin/easy_install-pypy
}

package_pypy3-setuptools() {
  depends=(pypy3${_pypy3abi})
  provides=(pypy3${_pypy3abi}-setuptools=${pkgver}-${pkgrel})
  cd pypy3-setuptools-${pkgver}
  pypy3 setup.py install \
        --root="${pkgdir}" --optimize=1 --skip-build

  cd "${pkgdir}"
  mkdir -p usr/bin
  ln -s ../../opt/pypy3/bin/easy_install usr/bin/easy_install-pypy3
}
