# Maintainer: ava1ar <mail(at)ava1ar(dot)info>
# Contributor: Dalton Miller
# Contributor: Kilian Lackhove kilian@lackhove.de
# Contributor: Justin Patera serialhex@gmail.com

pkgname=btsync
pkgver=1.3.106
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'arm' 'armv6' 'armv7' 'armv6h' 'armv7h')
url="http://www.bittorrent.com/sync"
install=btsync.install
backup=('etc/btsync.conf')
optdepends=("btsync-autoconfig: Auto-create users' config files if needed") 
source=(btsync.service
	btsync@.service
	btsync_user.service)
sha1sums=('9187bafe0cc30895d55b8a8a69074ebdca325e4c' 
	  'c4f87b9d65247f4a9033a6039662f4b863a5b03e'
	  'dd80af8594aef9bbeafa74b7c36b4b980c4c3751')
if [ "$CARCH" == x86_64 ]; then
        source+=(http://syncapp.bittorrent.com/${pkgver}/${pkgname}_x64-${pkgver}.tar.gz)
        sha1sums+=('68f5e0efbe153025759f3e83be77f08a8307ea22')
elif [ "$CARCH" == i686 ]; then
        source+=(http://syncapp.bittorrent.com/${pkgver}/${pkgname}_i386-${pkgver}.tar.gz)
        sha1sums+=('1bb8ffaa6be37d7ef0cffc0085ad3f18d0895893')
elif [[ "$CARCH" == arm ||  "$CARCH" == armv6 || "$CARCH" == armv6h || "$CARCH" == armv7 || "$CARCH" == armv7h ]]; then
        source+=(http://syncapp.bittorrent.com/${pkgver}/${pkgname}_arm-${pkgver}.tar.gz)
        sha1sums+=('eeead86fa1653164d35663a8350365c29d00dd55')
fi

package() {
	cd ${srcdir}
	install -Dm755 btsync ${pkgdir}/usr/bin/btsync
	# generate and install system-wide configand systemd unit
	mkdir -p ${pkgdir}/etc
	./btsync --dump-sample-config | sed 's:/home/user/\.sync:/var/lib/btsync:g' > ${pkgdir}/etc/btsync.conf
	# install systemd units
	install -Dm644 btsync.service ${pkgdir}/usr/lib/systemd/system/btsync.service
	install -Dm644 btsync@.service ${pkgdir}/usr/lib/systemd/system/btsync@.service
	install -Dm644 btsync_user.service ${pkgdir}/usr/lib/systemd/user/btsync.service
	# install license
	install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
