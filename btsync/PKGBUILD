# Maintainer: ava1ar <mail(at)ava1ar(dot)info>
# Contributor: Dalton Miller
# Contributor: Kilian Lackhove kilian@lackhove.de
# Contributor: Justin Patera serialhex@gmail.com

pkgname=btsync
pkgver=1.4.72
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'arm' 'armv6' 'armv7' 'armv6h' 'armv7h')
url="http://www.bittorrent.com/sync"
install=btsync.install
backup=('etc/btsync.conf')
optdepends=("btsync-autoconfig: Auto-create users' config files if needed") 
source=(btsync.service btsync_user.service)
sha1sums=('05038b03a673dd0207b2758a2e01db5d28c409d1' 'd30deb7e41ba1d163b4a5e442b8d8118758d312a')
if [ "$CARCH" == x86_64 ]; then
	_arch=x64
	sha1sums+=('0ce793b52d57a57fbe3817c1258fe8fd82030ebe')
elif [ "$CARCH" == i686 ]; then
	_arch=i386
	sha1sums+=('a047a4102b58d77cfeda2dad1f98d37641fa21ae')
elif [[ "$CARCH" == arm ||  "$CARCH" == armv6 || "$CARCH" == armv6h || "$CARCH" == armv7 || "$CARCH" == armv7h ]]; then
	_arch=arm
	sha1sums+=('18f2f323c1b733ed273502b22d110e2f43d0f653')
fi
source+=(${pkgname}_${_arch}-${pkgver}.tar.gz::http://download-new.utorrent.com/endpoint/btsync/os/linux-${_arch}/track/stable)

package() {
	cd ${srcdir}
	install -Dm755 btsync "${pkgdir}"/usr/bin/btsync

	# generate and install system-wide configand systemd unit
	mkdir -p "${pkgdir}"/etc
	./btsync --dump-sample-config | sed 's:/home/user/\.sync:/var/lib/btsync:g' > "${pkgdir}"/etc/btsync.conf

	# install systemd units
	install -Dm644 btsync.service "${pkgdir}"/usr/lib/systemd/system/btsync.service
	install -Dm644 btsync_user.service "${pkgdir}"/usr/lib/systemd/user/btsync.service

	# install license
	install -Dm644 LICENSE.TXT "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
