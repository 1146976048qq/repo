# Maintainer: ava1ar <mail(at)ava1ar(dot)info>
# Contributor: Dalton Miller
# Contributor: Kilian Lackhove kilian@lackhove.de
# Contributor: Justin Patera serialhex@gmail.com

pkgname=btsync
pkgver=1.4.103
pkgrel=2
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'arm')
url="http://www.bittorrent.com/sync"
install=btsync.install
backup=('etc/btsync.conf')
optdepends=("btsync-autoconfig: Auto-create users' config files if needed") 
source=(btsync.service btsync_user.service btsync.conf)
sha1sums=('05038b03a673dd0207b2758a2e01db5d28c409d1' 'd30deb7e41ba1d163b4a5e442b8d8118758d312a' 'bf7d6bd7ffe2d23cce51a51e92fac3b82a7c87bb')
source_arm+=("http://syncapp.bittorrent.com/${pkgver}/btsync_arm-${pkgver}.tar.gz")
source_i686+=("http://syncapp.bittorrent.com/${pkgver}/btsync_i386-${pkgver}.tar.gz")
source_x86_64+=("http://syncapp.bittorrent.com/${pkgver}/btsync_x64-${pkgver}.tar.gz")
sha1sums_arm+=('a31935cf13bb6fd0c7ef874025d226b2c9da0b31')
sha1sums_i686+=('7b8c9b86cbbbf98aecb1f2365a16282a09e4dc0d')
sha1sums_x86_64+=('a772dedfb0548de39f297c177fd80ddbc988cd67')

package() {
	cd ${srcdir}
	install -Dm755 btsync ${pkgdir}/usr/bin/btsync

	# generate and install system-wide config
	mkdir -p "${pkgdir}"/etc
	./btsync --dump-sample-config \
		| sed 's:/home/user/\.sync:/var/lib/btsync:' \
		| sed 's:\/\/ "pid_file":  "pid_file":' \
		| sed 's:\/\/ "storage_path":  "storage_path":' \
		> ${pkgdir}/etc/btsync.conf

	# install systemd config files
	install -Dm644 btsync.service ${pkgdir}/usr/lib/systemd/system/btsync.service
	install -Dm644 btsync.conf ${pkgdir}/usr/lib/tmpfiles.d/btsync.conf
	install -Dm644 btsync_user.service ${pkgdir}/usr/lib/systemd/user/btsync.service

	# install license
	install -Dm644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
