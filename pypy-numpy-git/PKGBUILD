_pypyname=pypy
_pypyabi=24

pkgname=${_pypyname}-numpy-git
pkgver=12394.33ef4b5
pkgrel=2
pkgdesc="Scientific tools for Python, pypy version."
url="https://bitbucket.org/pypy/numpy"
arch=('i686' 'x86_64')
license=('BSD')
depends=('lapack' 'blas' "pypy-${_pypyabi}")
makedepends=('git' 'gcc-fortran')
provides=('pypy-numpy' "pypy-${_pypyabi}-numpy")
conflicts=('pypy-numpy')
options=('debug' 'strip')
source=(git+https://bitbucket.org/pypy/numpy.git)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/numpy"

  echo "$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
  cd "$srcdir/numpy"

  export ATLAS=None
  export LDFLAGS="$LDFLAGS -shared"

  pypy setup.py config_fc --fcompiler=gnu95 build
}

_fix_numpy_cffi() {
  cd "${srcdir}"
  PYTHONPATH="${pkgdir}/opt/pypy/site-packages" pypy -c 'import numpy'
  rm -rf "${pkgdir}/opt/pypy/site-packages/numpy/fft/__pycache__/"*/
}

package() {
  cd "$srcdir/numpy"

  export ATLAS=None
  export LDFLAGS="$LDFLAGS -shared"

  pypy setup.py config_fc --fcompiler=gnu95 install --prefix=/opt/pypy \
    --root="$pkgdir" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/pypy-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/pypy-numpy/"

  install -m755 -d "${pkgdir}/opt/pypy/include/pypy"
  ln -sf /opt/pypy/site-packages/numpy/core/include/numpy \
    "${pkgdir}/opt/pypy/include/pypy/numpy"

  (_fix_numpy_cffi)
}
