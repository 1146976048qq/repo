# Maintainer: mutantmonkey <aur@mutantmonkey.in>
# Contributor: Techlive Zheng <techlivezheng@gmail.com>
pkgname=obfsproxy
pkgver=0.2.9
pkgrel=2
pkgdesc="A pluggable transport proxy written in Python"
arch=('any')
url="https://pypi.python.org/pypi/obfsproxy"
license=('BSD')
depends=('python2>=2.7' 'python2-crypto' 'python2-pyptlib>=0.0.6' 'twisted'
         'python2-yaml')
makedepends=('python2-setuptools')
optdepends=('python2-gmpy: speed up some cryptographic operations')
conflicts=('pyobfsproxy' 'obfsproxy-git')
options=(!emptydirs)
source=("https://pypi.python.org/packages/source/o/obfsproxy/obfsproxy-${pkgver}.tar.gz"{,.asc})
sha256sums=('603bde8db72324a7fdbd8c4abaebb7546f1515cdc7aedc1d7fef21d9c499de9f'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # argparse is part of python 2.7+, so we can remove it from install_requires
  sed -i "/'argparse',/d" setup.py
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
