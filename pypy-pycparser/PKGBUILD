# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: lang2 <wenzhi.liang@gmail.com>

_pypyname=pypy
_pypyabi=24
_pypy3name=pypy3
_pypy3abi=24

pkgbase=${_pypyname}-pycparser
pkgname=(${_pypy3name}-pycparser ${_pypyname}-pycparser)
pkgver=2.10
pkgrel=3
pkgdesc='C parser and AST generator written in Python'
url='https://github.com/eliben/pycparser'
makedepends=(pypy3-${_pypy3abi}-ply pypy-ply pypy3-${_pypy3abi})
arch=('any')
license=('BSD')
source=('https://github.com/eliben/pycparser/archive/release_v2.10.zip')
sha256sums=('1217244f882c330782f4762a1fb37cef21a929ce0123ac388e7e367c35ddbae3')

prepare() {
  cp -r pycparser-release_v${pkgver}{,-py2}
}

build() {
  (cd pycparser-release_v${pkgver}
   pypy3 setup.py build

   cd pycparser
   pypy3 _build_tables.py)

  (cd pycparser-release_v${pkgver}-py2
   pypy setup.py build

   cd pycparser
   pypy _build_tables.py)
}

package_pypy3-pycparser() {
  depends=(pypy3-${_pypy3abi}-ply pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-pycparser=${pkgver})
  cd pycparser-release_v${pkgver}

  pypy3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-pycparser() {
  depends=(pypy-ply)
  cd pycparser-release_v${pkgver}-py2

  pypy setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
