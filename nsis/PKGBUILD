# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andre Klitzing <andre () incubo () de>
pkgname=nsis
pkgver=2.46
pkgrel=5
pkgdesc='A professional open source system to create Windows installers'
arch=('i686' 'x86_64')
url='http://nsis.sourceforge.net'
license='custom:zlib'
depends=('mingw-w64-crt')
makedepends=('scons' 'mingw-w64-gcc' 'mingw-w64-binutils')
options=(!strip)
source=(http://downloads.sourceforge.net/project/nsis/NSIS%202/$pkgver/$pkgname-$pkgver-src.tar.bz2
        nsis-2.43-64bit-fixes.patch
        nsis-2.46-fixes.patch)
md5sums=('61c2e81739436b06d7cf7bcce1d533ac'
         '9eead3b78da54e3afda8f6a5b663aea9'
         '4ff92d59875414c9132cda02c15a3adc')

build() {
  cd "$srcdir/$pkgname-$pkgver-src"

  patch -p1 -i "$srcdir/nsis-2.46-fixes.patch"

  # Patch taken from
  # http://cvs.fedoraproject.org/viewvc/rpms/mingw32-nsis/F-11/nsis-2.43-64bit-fixes.patch
  patch -p1 -i "$srcdir/nsis-2.43-64bit-fixes.patch"
  sed -i '/^def find(env)/a\    return "/usr/bin/i686-w64-mingw32-"' \
    SCons/Tools/crossmingw.py
  sed -i '/ min(/i#define min(x, y) ((x)<(y)?(x):(y))' \
    Contrib/InstallOptions/InstallerOptions.cpp
  scons PREFIX_DEST="$pkgdir/" PREFIX=/usr/i686-w64-mingw32 SKIPUTILS='NSIS Menu'

  # Patch version from DD-MM-YYY.cvs to 2.46 (makes CPack working again)
  sed -i "s/'Version of NSIS', cvs_version)/'Version of NSIS', '${pkgver}')/" \
    "${srcdir}/${pkgname}-${pkgver}-src/SConstruct"
}

package() {
  cd "$srcdir/$pkgname-$pkgver-src"
  scons PREFIX_DEST="$pkgdir/" PREFIX=/usr/i686-w64-mingw32 SKIPUTILS='NSIS Menu' install

  # Add a symlink to 'makensis' for lazy people ;-)
  mkdir "$pkgdir/usr/bin/"
  cd "$pkgdir/usr/bin/"
  ln -s ../i686-w64-mingw32/bin/makensis

  cd "$srcdir/$pkgname-$pkgver-src"
  install -Dm644 Docs/src/license.but "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
