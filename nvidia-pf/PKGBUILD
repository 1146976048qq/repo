# Maintainer : Zuyi Hu <hzy068808@gmail.com>

pkgname=nvidia-pf
pkgver=396.54
_extramodules=extramodules-4.18-pf
pkgrel=2
pkgdesc="NVIDIA drivers for linux-pf"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-pf>=4.18' 'linux-pf<4.19' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-pf-headers>=4.18' 'linux-pf-headers<4.19')
license=('custom')
install=nvidia.install
options=(!strip)
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        )

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-modeset.ko" \
         "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-modeset.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-drm.ko" \
         "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-drm.ko"

    if [[ "$CARCH" = "x86_64" ]]; then
        install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-uvm.ko" \
            "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    fi

    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
}
md5sums=('195afa93d400bdbb9361ede6cef95143')
