# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-julia
pkgver=0.25.1
pkgrel=5
epoch=1
pkgdesc='A linkable library for Git'
arch=(i686 x86_64 armv7h aarch64)
url='https://libgit2.github.com/'
depends=(zlib openssl libssh2 curl http-parser)
makedepends=(cmake python)
provides=("libgit2=$epoch:$pkgver")
conflicts=(libgit2)
license=('GPL2')
source=("libgit2-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        libgit2-agent-nonfatal.patch
        0001-filebuf-fix-resolving-absolute-symlinks.patch
        0001-Do-not-free-config-when-creating-remote.patch)
sha1sums=('c65238d0e0a698b202a3a886d003228cac6dacc3'
          '5b333adbd3bfa5e546053d2439d15f684cb0c44f'
          '6411486c592b97d2c94147258c32dae4d0fddb19'
          '28e2cd4b5a3a264cb5a0fc771d483343884c0ad5')
options=(!strip)

prepare() {
  cd "libgit2-$pkgver"

  patch -p1 < ../libgit2-agent-nonfatal.patch
  patch -p1 < ../0001-filebuf-fix-resolving-absolute-symlinks.patch
  patch -p1 < ../0001-Do-not-free-config-when-creating-remote.patch
}

build() {
  cd "libgit2-$pkgver"
  export LANG=en_US.UTF-8
  export CFLAGS+=' -g'
  export CXXFLAGS+=' -g'
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

package() {
  cd "libgit2-$pkgver"
  make DESTDIR="$pkgdir" install
}
