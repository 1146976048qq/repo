# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-julia
pkgver=0.25.1
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=(i686 x86_64 armv7h aarch64)
url='https://libgit2.github.com/'
depends=(zlib openssl libssh2 curl)
makedepends=(cmake python)
provides=("libgit2=$epoch:$pkgver")
conflicts=(libgit2)
license=('GPL2')
source=("libgit2-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        libgit2-agent-nonfatal.patch
        libgit2-mbedtls-verify.patch
        libgit2-ssh.patch
        libgit2-mbedtls.patch
        libgit2-mbedtls-writer-fix.patch)
sha1sums=('c65238d0e0a698b202a3a886d003228cac6dacc3'
          '5b333adbd3bfa5e546053d2439d15f684cb0c44f'
          '025c5a0dd6dfcdadc63c0dbbbdac4ce951e09b3a'
          '966b68e9a6f50c8c89a36ca167cb895aecd0e0e5'
          '15199c782deddbcd372fdcdb26450bc376fb251d'
          '2abf3517fefa23eaa609efd27c6db52173793984')

prepare() {
  cd "libgit2-$pkgver"

  patch -p0 < ../libgit2-ssh.patch
  patch -p1 < ../libgit2-mbedtls.patch
  patch -p1 < ../libgit2-agent-nonfatal.patch
  patch -p1 < ../libgit2-mbedtls-writer-fix.patch
  patch -p1 < ../libgit2-mbedtls-verify.patch
}

build() {
  cd "libgit2-$pkgver"
  export LANG=en_US.UTF-8
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

package() {
  cd "libgit2-$pkgver"
  make DESTDIR="$pkgdir" install
}
