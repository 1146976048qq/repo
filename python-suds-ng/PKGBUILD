# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-suds-ng
pkgname=(python-suds-ng python2-suds-ng)
pkgver=0.4.1
pkgrel=1
pkgdesc="A lightweight SOAP python client - fork of suds"
arch=('any')
url="https://github.com/felixonmars/suds-ng"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=("https://pypi.python.org/packages/source/s/suds-ng/suds-ng-$pkgver.tar.gz")
md5sums=('3acbe1f011ed86fbd4470ce8b2547e62')

prepare() {
  cp -a suds-ng-$pkgver{,-py2}
}

build() {
  cd suds-ng-$pkgver
  python setup.py build

  cd ../suds-ng-$pkgver-py2
  python2 setup.py build
}

package_python-suds-ng() {
  depends=('python-six')

  cd suds-ng-$pkgver
  python setup.py install --root="${pkgdir}" -O1
}

package_python2-suds-ng() {
  depends=('python2-six')

  cd suds-ng-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" -O1
}

# vim:set ts=2 sw=2 et:
