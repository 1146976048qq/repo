pkgbase=firefox-nightly-i18n
pkgver=59.0a1_20180105.12
_pkgver=59.0a1
pkgrel=1
pkgdesc="Language pack for Firefox Nightly"
arch=('any')
url="https://www.mozilla.com/"
license=('MPL' 'GPL')

_languages=(
  'zh-CN  "Chinese (Simplified)"'
  'zh-TW  "Chinese (Traditional)"'
  'ja  Japanese'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/firefox/nightly/latest-mozilla-central-l10n/linux-x86_64/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=firefox-nightly-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("$pkgver-firefox-nightly-i18n-$_pkgver-$_locale.xpi::$_url/firefox-$_pkgver.$_locale.langpack.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Firefox"
  depends=("firefox-nightly")
  install -Dm644 $pkgver-firefox-nightly-i18n-$_pkgver-$1.xpi \
    "$pkgdir/opt/firefox-nightly/browser/extensions/langpack-$1@firefox.mozilla.org.xpi"
}
md5sums=('fe03df55573935f758f090c26457cf4d'
         'ae297db4f78f35012f572be38ac21e6b'
         'f5f27021f0372c08cf38b106c71c983c')
