# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=wireshark-gtk2
pkgbase=wireshark
pkgver=2.0.0
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('gtk2' 'krb5' 'libpcap' 'bash' 'gnutls' 'portaudio'
             'lua52' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
options=(!emptydirs)
url='http://www.wireshark.org/'
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2)
sha1sums=('f8b226b40e36e4c431b4f70bc3f523b6dd53a501')

build() {
  cd ${pkgbase}-${pkgver}

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-gtk2 \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5
  make all
}



package() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK 2 frontend'
  depends=('desktop-file-utils' 'gtk2' 'portaudio' 'wireshark-cli' 'wireshark-common')
  install=wireshark-gui.install

  cd ${pkgbase}-${pkgver}

  install -Dm755 .libs/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk2"
  install -Dm644 wireshark-gtk.desktop "${pkgdir}/usr/share/applications/wireshark-gtk2.desktop"
  sed -i -e 's/GTK+/&2/' -e 's/-gtk/&2/' \
         "${pkgdir}/usr/share/applications/wireshark-gtk2.desktop"
}

