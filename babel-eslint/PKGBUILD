pkgname=babel-eslint
pkgver=8.2.6
pkgrel=1
pkgdesc='allows you to lint ALL valid Babel code with the fantastic ESLint'
arch=('any')
url='https://github.com/babel/babel-eslint'
license=('MIT')
depends=('eslint')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('6827478e1cdc20b74ff1cf657a3ee1bd72af4227b246ddb4485d74d92206c98e1c5088b0e94680b4ae23da8ddd5d7ef88c49b2d1327408486fe1f4c83374b370')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/$pkgname/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
