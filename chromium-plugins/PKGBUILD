# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgbase=chromium-plugins
pkgname=(chromium-pepper-flash)
pkgdesc="Google Chrome's plugins for Chromium (pepper-flash stable version)"
pkgver=37.0.2062.94
pkgrel=1
epoch=1
_verbld=${pkgver}-1
_channel='stable'
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
source=(license.html::http://www.google.com/chrome/intl/en/eula_text.html)
sha1sums=('SKIP')
if [ "$CARCH" == x86_64 ]; then
	source+=(https://dl.google.com/linux/chrome/rpm/stable/x86_64/google-chrome-${_channel}-${_verbld}.x86_64.rpm)
	sha1sums+=('49d17ef07da6b31f7486e77367c5c25162afe2bd')
elif [ "$CARCH" == i686 ]; then
	source+=(https://dl.google.com/linux/chrome/rpm/stable/i386/google-chrome-${_channel}-${_verbld}.i386.rpm)
	sha1sums+=('03d2693861bfe1dc34769fbf1cbf8fb6254b527c')
fi

package_chromium-pepper-flash() {
	pkgdesc="Google Chrome's Pepper Flash plugin for Chromium (stable version)"
	pkgver=14.0.0.177
	conflicts=('chromium-pepper-flash-dev')
	install=chromium-pepper-flash.install

	install -d "${pkgdir}/usr/lib/PepperFlash"
	install -m644 opt/google/chrome/PepperFlash/* "${pkgdir}/usr/lib/PepperFlash"
	sed -i "s/flashver=.*/flashver=${pkgver}/" "${startdir}/chromium-pepper-flash.install"
	# License
	install -Dm644 "${srcdir}/license.html" "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
}
