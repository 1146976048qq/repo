# Maintainer: ykelvis < yk [at] archlinuxcn [dot] org >
# Original arch package <nvidia-340xx-ck> by: graysky <graysky AT archlnux.us>

pkgname=nvidia-340xx-mainline
pkgver=340.58
_extramodules=extramodules-3.18-mainline
pkgrel=1
pkgdesc="NVIDIA drivers for linux-mainline, 340xx legacy branch."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-mainline>=3.17' 'linux-mainline<3.19' "nvidia-340xx-libgl" "nvidia-340xx-utils=${pkgver}")
makedepends=('linux-mainline-headers>=3.17' 'linux-mainline-headers<3.19')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia-mainline')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run"
	    	"nv-drm.patch")
    md5sums=('b993f9fc59434140f560e32fd7bd2b3d'
	    	 '79671a27131da619a33eb02ed0c2c031')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run"
	    	"nv-drm.patch")
    md5sums=('26e75f80df3f49ac2fe35c65b9de709e'
	    	 '79671a27131da619a33eb02ed0c2c031')
fi

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
    patch kernel/nv-drm.c ../nv-drm.patch
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module

    cd uvm
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-mainline.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}