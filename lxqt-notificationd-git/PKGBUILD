# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-notificationd
pkgname=$_pkgname-git
pkgver=0.8.0.6.ga6b7185
pkgrel=2
pkgdesc="LXQt notification daemon and library."
arch=("i686" "x86_64" "armv6h")
url="http://lxqt.org"
license=("GPL2")
depends=("lxqt-common-git" "liblxqt-git" "kwindowsystem")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
