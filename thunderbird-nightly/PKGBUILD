# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=thunderbird
_channel=nightly
pkgbase=thunderbird-nightly
pkgname=()
_version=64.0a1
pkgver=64.0a1.20181013.10
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig'
         'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg'
         'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss'
         'shared-mime-info' 'sqlite' 'startup-notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-comm-central-l10n"
_filename_prefix="20181013.10-"
source=("${pkgbase}.desktop")
sha512sums=('ef9cf2a1aecda97587095c2096e6e7d27840da80ec0071341421cbb4bf4d2e06a01b5c95dac3f3c8ea520700035328f8e6becffa0243c1a8da8c6c710126d5a4'
            'c710cc02a71ed7e107aa50e08854f0f4bf259422e90475131b731dce8aafb6292971cf10d619683746cea1607f1015bdee9f6ddffe1a2230c39de57c5aec738a'
            'SKIP'
            'af842b5ce99718befe16d495859947e8624a9385e371d04561b386d67a91e49841caaf5a06f8de7df11907eb208832087bfb30e02471f415dd189ff7fd1afb93'
            'SKIP'
            '0b9e8cbb0459a00a03c49e4cd3e5dcd48c1a99388a33b193f9045ba4616af45651d7d557a10d882c7febcf43625629d704d290bb476d056764614f8c9ba8f22d'
            'SKIP'
            '7020021369e99dc3e4a308b95294c7f753a09c2a6ab5b6700e8f564dda9f25dec309a7f0f410a805cd685bcc4e4b83a8908615c270527096869d01dcd54b807a'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=thunderbird-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.bz2"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.bz2"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.bz2.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old thunderbird directory...'
    rm -rf thunderbird
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(thunderbird-${_channel}=$pkgver)
  conflicts=(thunderbird-${_channel} thunderbird-${_channel}-i18n-${1,,})
  replaces=(thunderbird-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
EOF

  # Disable auto-updates
  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF

  # Use system-provided dictionaries
  rm -rf "${pkgdir}"/${OPT_PATH}/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "${pkgdir}"/${OPT_PATH}/dictionaries
  ln -sf /usr/share/hyphen "${pkgdir}"/${OPT_PATH}/hyphenation
}
