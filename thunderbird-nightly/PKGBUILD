# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=thunderbird
_channel=nightly
pkgbase=thunderbird-nightly
pkgname=()
_version=65.0a1
pkgver=65.0a1.20181110.10
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig'
         'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg'
         'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss'
         'shared-mime-info' 'sqlite' 'startup-notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-comm-central-l10n"
_filename_prefix="20181110.10-"
source=("${pkgbase}.desktop")
sha512sums=('ef9cf2a1aecda97587095c2096e6e7d27840da80ec0071341421cbb4bf4d2e06a01b5c95dac3f3c8ea520700035328f8e6becffa0243c1a8da8c6c710126d5a4'
            '8f01afc03f0ef351f0f08babd92caf1d98cd3fc47535786556ac35e1f84caf2b738672899f2435cc82b3cefe14e42982b6d74668806a01396dc4eb5e9730bbe6'
            'SKIP'
            '5f9b65d5a02390c9c05c1326ef308e32328051f424f435b1352297e33b692d53462aff41c242e62fb19b476d8589bb84cd5ae4195eb998318c258d151edd4eba'
            'SKIP'
            '5e69e016aa3365a7ee5a6c4ba2bf4cdecc5bb3cce47ef8a1e70e808d575c5123b6dcefd1023b79da9232b1e4198a88166255f019de9bff892028c8d498f25725'
            'SKIP'
            'ce27eb161de1ff6bbe601a52ab6a7a7362d04e2c16f6b51558606f3a716ef1181f66952c2436d0093bc49dbd0e349c36dc3c5d78b071f68805fe8841db1bac6b'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=thunderbird-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.bz2"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.bz2"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.bz2.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old thunderbird directory...'
    rm -rf thunderbird
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(thunderbird-${_channel}=$pkgver)
  conflicts=(thunderbird-${_channel} thunderbird-${_channel}-i18n-${1,,})
  replaces=(thunderbird-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
EOF

  # Disable auto-updates
  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF

  # Use system-provided dictionaries
  rm -rf "${pkgdir}"/${OPT_PATH}/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "${pkgdir}"/${OPT_PATH}/dictionaries
  ln -sf /usr/share/hyphen "${pkgdir}"/${OPT_PATH}/hyphenation
}
