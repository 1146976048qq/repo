# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Peter Baldwin <bald_pete@hotmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-jinja
pkgname=(${_pypy3name}-jinja ${_pypyname}-jinja)
pkgver=2.7.3
pkgrel=5
pkgdesc="A simple pythonic template language written in Python"
arch=('any')
url="http://jinja.pocoo.org/"
license=('BSD')
makedepends=(pypy-setuptools pypy3-${_pypy3abi}-setuptools pypy-markupsafe
             pypy3-${_pypy3abi}-markupsafe pypy3-${_pypy3abi})
source=(http://pypi.python.org/packages/source/J/Jinja2/Jinja2-$pkgver.tar.gz)
sha256sums=('2e24ac5d004db5714976a04ac0e80c6df6e47e98c354cb2c0d82f8879d4f8fdb')

build() {
  rm -rf pypy{,3}-build

  for pypy in pypy{,3}; do
    (cp -r Jinja2-$pkgver $pypy-build
     cd $pypy-build
     $pypy setup.py build)
  done
}

package_pypy3-hg-jinja() {
  depends=(pypy3-${_pypy3abi}-markupsafe pypy3-${_pypy3abi})
  provides=(pypy3-jinja=${pkgver}-${pkgrel}
            pypy3-${_pypy3abi}-jinja=${pkgver}-${pkgrel})
  conflicts=(pypy3-jinja)

  cd pypy3-build

  pypy3 setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-hg-jinja() {
  depends=(pypy-markupsafe pypy)
  provides=(pypy-jinja=${pkgver}-${pkgrel})
  conflicts=(pypy-jinja)

  cd pypy-build

  pypy setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
