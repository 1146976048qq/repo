pkgbase=pypy-pytest
pkgname=('pypy3-pytest' 'pypy-pytest')
pkgver=2.6.3
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('pypy3-setuptools' 'pypy-setuptools' 'pypy3-py' 'pypy-py')
source=("http://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")
sha512sums=('44fe4c7af804530160a1827b837d35be9492eebb6a382391c820568f492ce7ca50837a47e92873b014ee1655d83a8cb270e0c4149e683efedc25135791733c7a')

prepare() {
  cp -a pytest-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/pytest-${pkgver}"
  pypy3 setup.py build

  cd "$srcdir/pytest-${pkgver}-py2"
  pypy setup.py build
}

package_pypy3-pytest() {
  depends=("pypy3>=2.3" "pypy3<=2.4" 'pypy3-py' 'pypy3-setuptools')

  cd pytest-${pkgver}
  pypy3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/pypy3/bin/py.test" "${pkgdir}/usr/bin/pypy.test3"
}

package_pypy-pytest() {
  depends=('pypy' 'pypy-py' 'pypy-setuptools')

  cd pytest-${pkgver}-py2
  pypy setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/pypy/bin/py.test" "${pkgdir}/usr/bin/pypy.test"
}
