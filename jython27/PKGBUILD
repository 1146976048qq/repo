# $Id: PKGBUILD 67407 2012-03-11 12:49:30Z cbrannon $
# Maintainer: mitsuse <mitsuset at gmail>
## Original ##
#### Maintainer: Chris Brannon <cmbrannon79@gmail.com>
#### Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
#### Contributor: Richard Murri <admin@richardmurri.com>
pkgname=jython27
pkgver=2.7b2
_pkgver=2.7-b2
pkgrel=1
pkgdesc="An implementation of the Python language written in Java"
arch=('any')
url="http://www.jython.org/"
license=('PSF' 'APACHE' 'custom')
depends=('java-environment' 'bash')
provides=("jython=$pkgver")
backup=('opt/jython27/registry')
options=('!emptydirs')
install='jython.install'
source=(http://search.maven.org/remotecontent?filepath=org/python/jython-installer/$_pkgver/jython-installer-$_pkgver.jar
        'README.ArchLinux')
noextract=(jython-installer-${_pkgver}.jar)

package() {
	cd "$srcdir"
	java -jar jython-installer-${_pkgver}.jar -s -t standard -d "$pkgdir"/opt/"$pkgname"
	sed -i s*"${pkgdir}"**g "${pkgdir}"/opt/"${pkgname}"/jython
	# Get rid of hard-coded JAVA_HOME, since all the JDKs don't use
	# the same directory.
	sed -i '/^JAVA_HOME=.*$/d' "${pkgdir}"/opt/$pkgname/jython
	install -m755 -D "$pkgdir/opt/$pkgname/jython" "$pkgdir/usr/bin/$pkgname"
	install -m644 -D "$srcdir/README.ArchLinux" \
	    "$pkgdir/usr/share/doc/$pkgname/README.ArchLinux"
	rm "$pkgdir/opt/$pkgname/jython" "$pkgdir/opt/$pkgname/bin/jython"
	install -m644 -D "$pkgdir/opt/$pkgname/LICENSE.txt" \
	    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

sha512sums=('58638b975d8b729c5f613be29372d33f7c54c06ba4eaf2e3f11044376e506e82e594b137528ed4bdb5940e71dff7656e39eae60ce73b5de92012006ae35230b2'
            '0ab0237d456f859b2cbb0739264bb978ad98b2c52153e89fc131aa60edf7837f41617c0e53cbde05688dfc0d6fb08f1d02ebbd41da99b07af72e34a11a83f0b9')
