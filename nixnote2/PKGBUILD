# Maintainer: Tom Hale <tom[noodle]hale[point]ee>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=nixnote2
pkgver=2.1.0
pkgrel=1
pkgdesc='Evernote clone (formerly Nevernote) - git checkout'
url="https://github.com/robert7/$pkgname"
arch=(x86_64)
license=(GPL3)
depends=(hunspell java-runtime hicolor-icon-theme poppler-qt5 tidy qt5-webkit)
makedepends=(boost gcc git qt5-tools)
conflicts=(nixnote2-git)
replaces=(nevernote nixnote nixnote-beta)
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('fc946e527e2205ea7cb75d25ff3fc1da6e5676898dc60126d2cc9db54c35b83f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake PREFIX=/usr
  make -j $(( 1 + ($(nproc)-1) / 2 ))  # Use ceil(#processors / 2) jobs
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}/" CONTRIBUTING.md debian/changelog
  # shortcuts.txt is referenced in doc:
  # https://github.com/robert7/nixnote2/wiki/HowTo---Customize-keyboard-shortcuts
  install -Dm644 -t "${pkgdir}/usr/share/${pkgname}/" shortcuts.txt
}
