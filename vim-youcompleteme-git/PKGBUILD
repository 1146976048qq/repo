# Maintainer: Erik Beran <eberan AT_gmail_DOT com>
# Contributor: Stephen Zhang <zsrkmyn at gmail dot com>
# Contributor: Thor K. H. <thor at roht dot no>
# Contributor: Babken Vardanyan <483ken 4tgma1l
# Contributor: mikezackles
# Contributor: z33ky
# Contributor: stykr
# Contributor: Svenstaro
# Contributor: KaiSforza
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: shmilee
# Contributor: foobster
# Contributor: archdria
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-youcompleteme-git
pkgver=1391.ba6e140
pkgver() {
  cd "YouCompleteMe"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}
pkgrel=1
pkgdesc="A code-completion engine for Vim"

arch=('i686' 'x86_64')
url='http://valloric.github.com/YouCompleteMe/'
license=('GPL3')
groups=('vim-plugins')
depends=('python2' 'vim')
optdepends=('mono: for C# completer'
            'nodejs: for nodejs completer'
            'rust: for rust completer'
            'clang: for C/C++/Obj-C completer')
makedepends=('cargo' 'cmake' 'git' 'go' 'make' 'mono' 'npm' 'nodejs' 'rust' 'clang')
source=('git+https://github.com/Valloric/YouCompleteMe.git'
        'git+https://github.com/kennethreitz/requests.git'
        'git+https://github.com/ross/requests-futures.git'
        'git+https://github.com/Valloric/ycmd.git'
        'git+https://github.com/bewest/argparse.git'
        'git+https://github.com/bottlepy/bottle.git'
        'git+https://github.com/slezica/python-frozendict.git'
        'git+https://github.com/davidhalter/jedi.git'
        'git+https://github.com/vheon/JediHTTP.git'
        'git+https://github.com/Pylons/waitress.git'
        'git+https://github.com/nsf/gocode.git'
        'git+https://github.com/Manishearth/godef.git'
        'git+https://github.com/nosami/OmniSharpServer.git'
        'git+https://github.com/icsharpcode/NRefactory.git'
        'git+https://github.com/jbevain/cecil.git'
        'git+https://github.com/jwilm/racerd.git'
        'git+https://github.com/PythonCharmers/python-future.git')
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
install=install
_COMPLETER="USE_CLANG_COMPLETER"
_SYSTEM_LIBCLANG="USE_SYSTEM_LIBCLANG"

prepare() {
  msg2 'Setting up Git submodules...'

  cd "$srcdir/YouCompleteMe"
  git submodule init
  git config submodule.third_party/requests-futures.url "$srcdir/requests-futures"
  git config submodule.third_party/ycmd.url "$srcdir/ycmd"
  git submodule update

  cd "$srcdir/YouCompleteMe/third_party/ycmd"
  git submodule init
  git config submodule.third_party/argparse.url "$srcdir/argparse"
  git config submodule.third_party/bottle.url "$srcdir/bottle"
  git config submodule.third_party/frozendict.url "$srcdir/python-frozendict"
  git config submodule.third_party/JediHTTP.url "$srcdir/JediHTTP"
  git config submodule.third_party/waitress.url "$srcdir/waitress"
  git config submodule.third_party/gocode.url "$srcdir/gocode"
  git config submodule.third_party/godef.url "$srcdir/godef"
  git config submodule.third_party/OmniSharpServer.url "$srcdir/OmniSharpServer"
  git config submodule.third_party/requests.url "$srcdir/requests"
  git config submodule.third_party/racerd.url "$srcdir/racerd"
  git config submodule.third_party/python-future.url "$srcdir/python-future"
  git submodule update

  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/JediHTTP"
  git submodule init
  git config submodule.vendor/waitress.url "$srcdir/waitress"
  git config submodule.vendor/jedi.url "$srcdir/jedi"
  git config submodule.vendor/bottle.url "$srcdir/bottle"
  git submodule update

  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/OmniSharpServer"
  git submodule init
  git config submodule.NRefactory.url "$srcdir/NRefactory"
  git config submodule.cecil.url "$srcdir/cecil"
  git submodule update
}

build() {
  # https://github.com/Valloric/ycmd/blob/master/build.py {{{

  msg2 'Building ycmd...' # BuildYcmdLibs()
  mkdir -p "$srcdir/ycmd_build"
  cd "$srcdir/ycmd_build"
  cmake -G "Unix Makefiles" -D${_COMPLETER}=1 -D${_SYSTEM_LIBCLANG}=1 . "$srcdir/YouCompleteMe/third_party/ycmd/cpp"
  make ycm_core

  msg2 'Building OmniSharp completer...' # BuildOmniSharp()
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/OmniSharpServer"
  pwd
  xbuild /property:Configuration=Release

  msg2 'Building Gocode completer...' # BuildGoCode()
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/gocode"
  pwd
  go build
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/godef"
  pwd
  go build

  msg2 'Building Rust completer...' # BuildRacerd()
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/racerd"
  pwd
  cargo build --release

  msg2 'Building Tern completer...' # SetUpTern()
  cd "$srcdir/YouCompleteMe/third_party/ycmd/third_party/tern_runtime"
  pwd
  npm install --production --python=python2
  # }}}
}

package() {
  mkdir -p "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/OmniSharpServer/OmniSharp/bin/Release"
  mkdir -p "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/gocode"
  mkdir -p "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/godef"
  mkdir -p "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/racerd/target/release"

  cp -r "$srcdir/YouCompleteMe/"{autoload,doc,plugin,python} \
    "$pkgdir/usr/share/vim/vimfiles"
  cp -r "$srcdir/YouCompleteMe/third_party/"{pythonfutures,requests-futures,retries} \
    "$pkgdir/usr/share/vim/vimfiles/third_party"
  cp -r "$srcdir/YouCompleteMe/third_party/ycmd/"{ycmd,ycm_core.so,check_core_version.py,CORE_VERSION} \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd"

  cp -r "$srcdir/YouCompleteMe/third_party/ycmd/third_party/"{argparse,bottle,frozendict,JediHTTP,requests,tern_runtime,waitress,python-future} \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party"

  cp    "$srcdir/YouCompleteMe/third_party/ycmd/third_party/gocode/gocode" \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/gocode/gocode"
  cp    "$srcdir/YouCompleteMe/third_party/ycmd/third_party/godef/godef" \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/godef/godef"
  cp -r "$srcdir/YouCompleteMe/third_party/ycmd/third_party/OmniSharpServer/OmniSharp/bin/Release" \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/OmniSharpServer/OmniSharp/bin"
  cp    "$srcdir/YouCompleteMe/third_party/ycmd/third_party/racerd/target/release/racerd" \
    "$pkgdir/usr/share/vim/vimfiles/third_party/ycmd/third_party/racerd/target/release/racerd"

  find "$pkgdir" -name .git -exec rm -fr {} +
}

# vim:set ts=2 sw=2 et:
