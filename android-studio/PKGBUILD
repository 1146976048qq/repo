# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Christoph Brill <egore911-at-gmail-dot-com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=android-studio
pkgver=0.4.3
pkgrel=1
_build=133.988757
pkgdesc="A new Android development environment based on IntelliJ IDEA."
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/installing/studio.html"
license=('APACHE')
depends=('android-sdk' 'fontconfig' 'java-environment' 'libxrender' 'mesa' 'python')
makedepends=('unzip' 'xorg-server-xvfb' 'ttf-dejavu')
optdepends=('android-sdk-platform-tools' 'android-sdk-build-tools')
conflicts=('android-studio-beta' 'android-studio-dev' 'android-studio-canary')
options=('!strip')
install=$pkgname.install
source=(
	"http://dl.google.com/dl/android/studio/ide-zips/$pkgver/android-studio-ide-$_build-linux.zip"
	"$pkgname.desktop")
sha512sums=(
	'138b6eccc3f5c9271b6229b1231718272ac6d04ec17c7b65691d1fba1ed60efa3d70fceee45460b1f402076dfe0f88b3e72632e9ae126475587bd7847678c81b'
	'fa3567f328af45d265c4fabf3e41b55d8e8cccfa9675e745f07dd6ae41950dd53a2ef41ef1caee86643f5c2ddf7a7681ee17155e208a7b6fdae6c0537dfc0c94')

[ $CARCH = "x86_64" ] && depends=('python' 'lib32-fontconfig' 'lib32-mesa' 'lib32-libxrender' 'android-sdk')

prepare() {
	cd $srcdir/$pkgname

	# extract the application icon
	unzip -qo lib/resources.jar artwork/icon_AS_128.png

	# enable anti aliasing
	echo "-Dswing.aatext=true" >> studio.vmoptions
	echo "-Dswing.aatext=true" >> studio64.vmoptions
}

package() {
	cd $srcdir/$pkgname

	# application stuff
	install -d $pkgdir/{opt/$pkgname,usr/bin}
	cp -a bin lib plugins $pkgdir/opt/$pkgname
	ln -s /opt/android-studio/bin/studio.sh $pkgdir/usr/bin/android-studio

	# starter stuff
	install -Dm655 artwork/icon_AS_128.png $pkgdir/usr/share/pixmaps/$pkgname.png
	install -Dm655 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

	chmod -R ugo+rX $pkgdir/opt
}