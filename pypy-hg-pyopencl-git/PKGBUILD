# $Id: PKGBUILD 108477 2014-03-27 14:48:11Z fyan $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

# pyopencl requires numpy
BUILD_PYPY3=0

pkgbase=pypy-hg-pyopencl-git
_pypy2_deps=("pypy>=2.5" "pypy<2.6")
_pypy3_deps=("pypy3>=2.5" "pypy3<2.6")
if ((BUILD_PYPY3)); then
    pkgname=('pypy3-hg-pyopencl-git' 'pypy-hg-pyopencl-git')
    makedepends=('pypy-setuptools' 'pypy3-setuptools' 'libcl>=1.2'
                 'opencl-headers>=2:1.2' 'mesa' 'pypy-mako' 'pypy3-mako'
                 'pypy-numpy' 'pypy3-numpy' 'git' "${_pypy2_deps[@]}"
                 "${_pypy3_deps[@]}")
else
  pkgname=('pypy-hg-pyopencl-git')
  makedepends=('pypy-setuptools' 'libcl>=1.2' 'opencl-headers>=2:1.2'
               'mesa' 'pypy-mako' 'pypy-numpy' 'git' "${_pypy2_deps[@]}")
fi
pkgver=2014.1.0.376.gc68089b
pkgrel=1
pkgdesc="A complete, object-oriented language binding of OpenCL to Python. PyPy version on top of cffi"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyopencl"
license=('custom')
epoch=1
source=('git://github.com/yuyichao/pyopencl#branch=cffi-test'
        'LICENSE.txt' 'cc')
sha1sums=('SKIP'
          '2e6966b3d9b15603ce2c3ff79eeadd63c5d066b7'
          '70e53f2b3b32efe852c9d2c266f49dab16df9b20')
options=('debug' 'strip')

prepare() {
  cd "$srcdir/pyopencl"
  git submodule init
  git submodule sync
  git submodule update
}

pkgver() {
  cd "$srcdir/pyopencl"

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  rm -rf "${srcdir}/pyopencl-python2"
  cd "$srcdir"

  cp -a pyopencl{,-python2}

  # hack to use g++ since gcc doesn't link to the required c++ runtime libraries.
  export PATH="${srcdir}:${PATH}"

  if ((BUILD_PYPY3)); then
      cd "${srcdir}/pyopencl"
      pypy3 ./configure.py --cl-enable-gl
      pypy3 setup.py build
  fi

  cd "$srcdir/pyopencl-python2"
  pypy ./configure.py --cl-enable-gl
  pypy setup.py build
}

package_pypy3-hg-pyopencl-git() {
  depends=('libcl>=1.2' 'opencl-headers12' 'mesa' "${_pypy3_deps[@]}"
           'pypy3-mako' 'pypy3-pytools')

  cd "${srcdir}/pyopencl"
  pypy3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "$srcdir"/LICENSE.txt \
          "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_pypy-hg-pyopencl-git() {
  depends=('libcl>=1.2' 'opencl-headers12' 'mesa' "${_pypy2_deps[@]}"
           'pypy-numpy' 'pypy-mako' 'pypy-pytools' 'gcc-libs')

  cd "${srcdir}/pyopencl-python2"
  pypy setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "$srcdir"/LICENSE.txt \
          "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
