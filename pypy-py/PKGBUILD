pkgbase=pypy-py
pkgname=('pypy3-py' 'pypy-py')
pkgver=1.4.25
pkgrel=2
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
_pypy3_deps=('pypy3>=2.4' 'pypy3<2.5')
makedepends=('pypy-setuptools' 'pypy3-setuptools' "${_pypy3_deps[@]}")
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")
sha512sums=('7e24a9d57298cf98fc246fbda7db81f6cd9d16b78425266c24d00ab1db77146ffab38820630b2dddb49bca7d864c2366d45fdb3e81bb284e509a309d4c1b5b98')

prepare() {
  cp -a py-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/py-${pkgver}"
  pypy3 setup.py build

  cd "$srcdir/py-${pkgver}-py2"
  pypy setup.py build
}

package_pypy3-py() {
  depends=("${_pypy3_deps[@]}")

  cd py-${pkgver}
  pypy3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_pypy-py() {
  depends=('pypy')

  cd py-${pkgver}-py2
  pypy setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
