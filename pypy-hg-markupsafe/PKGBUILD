# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alex Anthony <alex.anthony28991@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-markupsafe
pkgname=(${_pypy3name}-markupsafe ${_pypyname}-markupsafe)
pkgver=0.23
pkgrel=7
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('any')
url="http://pypi.python.org/pypi/MarkupSafe"
license=('custom')
makedepends=(pypy-setuptools pypy3-${_pypy3abi}-setuptools
             pypy pypy3-${_pypy3abi})
source=("http://pypi.python.org/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz")
sha512sums=('4f1fd91ced5e7119584b56cf7b69cfe6fdd9613bd77412368a38e9ef5d1011ba5c76d1d3a0da3d60f9f474627e6c8c8b613a80a668b32d212f09072f8b1f5b28')

build() {
  cp -r MarkupSafe-${pkgver} python2-MarkupSafe-${pkgver}

  (cd MarkupSafe-${pkgver}
   pypy3 setup.py build)

  (cd python2-MarkupSafe-${pkgver}
   pypy setup.py build)
}

package_pypy3-hg-markupsafe() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-markupsafe=${pkgver}-${pkgrel}
            pypy3-markupsafe=${pkgver}-${pkgrel})
  conflicts=(pypy3-markupsafe)

  cd MarkupSafe-${pkgver}
  pypy3 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE \
          "${pkgdir}/usr/share/licenses/pypy3-markupsafe/LICENSE"
}

package_pypy-hg-markupsafe() {
  depends=(pypy)
  provides=(pypy-markupsafe=${pkgver}-${pkgrel})
  conflicts=(pypy-markupsafe)

  cd python2-MarkupSafe-${pkgver}
  pypy setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE \
          "${pkgdir}/usr/share/licenses/pypy-markupsafe/LICENSE"
}
