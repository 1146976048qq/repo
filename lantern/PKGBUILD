# Submitter: neo.shadowsocks<neo.shadowsocks AT gmx.com>
# Maintainer: Kars Wang <jaklsy g-mail>

pkgname=lantern
pkgver=2.1.1
pkgrel=1
pkgdesc='Lantern is a free desktop application that delivers fast, reliable and secure access to the open Internet. (Stable Channel)'
arch=('i686' 'x86_64')
url='https://getlantern.org'
license=('Apache')

# neo: I got it by ldd the lantern-binary as deb metadata doesn't provides it,
# and remove duplicated, like atk required by gtk3
depends=('hicolor-icon-theme' 'bzip2' 'dbus' 'glib2' 'gtk3' 'libappindicator-gtk3' 'mesa' 'xcb-util' 'xdg-utils' 'zlib')
options=('!emptydirs' '!strip' '!docs')
install=$pkgname.install
source=('lantern.service')
source_i686=("lantern-installer-32-bit-$pkgver.deb::https://raw.githubusercontent.com/getlantern/lantern-binaries/master/lantern-installer-32-bit.deb")
source_x86_64=("lantern-installer-64-bit-$pkgver.deb::https://raw.githubusercontent.com/getlantern/lantern-binaries/master/lantern-installer-64-bit.deb")
sha512sums=('8157c1cdf16b96f15c47626e96d8f0c9f9b974ffcd4ac0137b09ced03878edf9bc4f6c6d00f91cc8be159a068d3f61e2fb16b90225b399cede4fa6daa21a251e')
sha512sums_i686=('aa75ed9f0be6bb80fe21e7b842c95123a3e1282a08dccfd06efdec5cb5d1968cc4e2519386c1c39962cd209917e9f71bf0bf6faeee33014627f640c95a53d58e')
sha512sums_x86_64=('077b4acdf5f1df9d4c1d4e1f0d1de44d61b2b5bc9b1399476ba012a75f31e3c15ce2c7d3344a5d6e8891585b97434f3de9f58c381e13c756af2074d99fc1ef90')

package() {
    msg2 'Extracting the data.tar.xz...'
    bsdtar -xf data.tar.xz -C "${pkgdir}/"

    msg2 'Install systemd service file...'
    install -Dm644 "${srcdir}/lantern.service" "${pkgdir}/usr/lib/systemd/system/lantern.service"
    install -dm750 "$pkgdir/var/lib/lantern"
}
