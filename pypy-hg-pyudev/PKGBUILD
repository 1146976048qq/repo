# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: examon <examon.mail[at]gmail[dot]com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Dwight Schauer <dschauer@ti.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pyudev
pkgname=(${_pypy3name}-pyudev ${_pypyname}-pyudev)
pkgver=0.16.1
pkgrel=4
arch=('any')
url='http://pyudev.readthedocs.org/en/latest/index.html'
license=('LGPL')
makedepends=(pypy pypy3-${_pypy3abi}
             pypy3-${_pypy3abi}-setuptools pypy-setuptools)
source=("http://pypi.python.org/packages/source/p/pyudev/pyudev-${pkgver}.tar.gz")
md5sums=('4034de584b6d9efcbfc590a047c63285')

build() {
  cp -rf "${srcdir}/pyudev-${pkgver}" "${srcdir}/py2udev-${pkgver}"
}

package_pypy3-hg-pyudev() {
  pkgdesc='A pure Python 3.x binding to libudev'
  depends=(udev pypy3-${_pypy3abi})
  provides=(pypy3-pyudev=${pkgver}-${pkgrel}
            pypy3-${_pypy3abi}-pyudev=${pkgver}-${pkgrel})
  conflicts=(pypy3-pyudev)
  cd pyudev-${pkgver}

  pypy3 setup.py install --root "${pkgdir}" --optimize=1
}

package_pypy-hg-pyudev() {
  pkgdesc='A pure Python 2.x binding to libudev'
  depends=(udev pypy)
  provides=(pypy-pyudev=${pkgver}-${pkgrel})
  conflicts=(pypy-pyudev)
  cd py2udev-${pkgver}

  pypy setup.py install --root "${pkgdir}" --optimize=1
}
