# Maintainer: dequis <dx@dxzone.com.ar>
# Previous maintainer: Joel Teichroeb <joel@teichroeb.net>

pkgname=rr-git
pkgver=4.2.0.r391.g13e2518
pkgrel=1
pkgdesc='a nondeterministic debugger'
arch=(i686 x86_64)
url='http://rr-project.org/'
license=('custom')
depends=('python2-pexpect' 'gdb')
makedepends=('git' 'cmake')
if [[ $CARCH = x86_64 ]]; then
    makedepends+=('gcc-multilib')
fi
source=('git://github.com/mozilla/rr'
        '0001-Add-an-option-to-skip-recording-when-running-under-r.patch')
sha1sums=('SKIP'
          'c2f81fd83f9a4931e3ae62ff09e5fd1c0474c169')
provides=('rr')
conflicts=('rr')
options=('debug' '!strip')

pkgver() {
  cd rr
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd rr
  patch -Np1 < ../0001-Add-an-option-to-skip-recording-when-running-under-r.patch
}

build() {
  cd rr
  mkdir -p build

  cd build
  # Debug build has `Werror` on
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd rr/build
  make DESTDIR="${pkgdir}" install
  install -D ../LICENSE "${pkgdir}/usr/share/licenses/rr/LICENSE"
}
