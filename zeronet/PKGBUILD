# Maintainer: redfish <redfish at galactica dot pw>

pkgname='zeronet'
pkgver=0.5.0.r122.5103879
pkgrel=1
_commit='5103879471176fced494dc70d35e8384275f787e'
arch=('any')
url="https://zeronet.io/"
depends=('python2>=2.7.10'
         'python2-gevent'
         'python2-msgpack'
		)
makedepends=('git')
license=('GPL2')
pkgdesc="Decentralized websites using Bitcoin crypto and the BitTorrent network."
source=("git+https://github.com/HelloZeroNet/ZeroNet.git#commit=$_commit"
        "zeronet.conf"
        "zeronet.service")
md5sums=('SKIP'
         'c5216860cfc435a4861c55fd3933391c'
         '4b1a25fc9f2cc691d40611ec4bfad99f')
install="zeronet.install"
backup=("etc/zeronet.conf")
options=(!strip) # ignore test binaries in the depsendency libs that fail strip

# Upstream uses camel case
_pkgarchive="ZeroNet"

package() {
   mkdir -p "$pkgdir/opt/zeronet"
   mkdir -p "$pkgdir/var/lib/zeronet"
   mkdir -p "$pkgdir/var/log/zeronet"

   # There is no setup.py shipped, so brute-force copy
   cp -a "$srcdir/$_pkgarchive/." "$pkgdir/opt/zeronet/"
   python2 -m compileall $pkgdir/opt/zeronet

   install -D -m644 "$srcdir/zeronet.conf" "$pkgdir/etc/zeronet.conf"
   install -D -m644 "$srcdir/zeronet.service" "$pkgdir/usr/lib/systemd/system/zeronet.service"

   install -D -m644 "$srcdir/$_pkgarchive/LICENSE" "$pkgdir/usr/share/licenses/$_pkgarchive/LICENSE"
}
