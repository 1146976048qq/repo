# $Id: PKGBUILD 108452 2014-03-27 14:47:51Z fyan $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Peter Baldwin <bald_pete@hotmail.com>

pkgbase='pypy-jinja'
pkgname='pypy-jinja'
pkgname=('pypy3-jinja' 'pypy-jinja')
pkgver=2.7.3
pkgrel=3
pkgdesc="A simple pythonic template language written in Python"
arch=('any')
url="http://jinja.pocoo.org/"
license=('BSD')
makedepends=('pypy-setuptools' 'pypy3-setuptools' 'pypy-markupsafe'
             'pypy3-markupsafe')
source=(http://pypi.python.org/packages/source/J/Jinja2/Jinja2-$pkgver.tar.gz)
sha256sums=('2e24ac5d004db5714976a04ac0e80c6df6e47e98c354cb2c0d82f8879d4f8fdb')

build() {
  cd "$srcdir"

  rm -rf pypy{,3}-build
  for builddir in pypy{,3}-build; do
    cp -r Jinja2-$pkgver $builddir
    pushd $builddir
    ${builddir%-build} setup.py build
    popd
  done
}

package_pypy3-jinja() {
  depends=('pypy3-markupsafe' "pypy3>=2.4" "pypy3<2.5")

  cd "$srcdir/pypy3-build"

  pypy3 setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-jinja() {
  depends=('pypy-markupsafe' "pypy")

  cd "$srcdir/pypy-build"

  pypy setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
