# Maintainer: Calimero <calimerotekni@free.fr>

pkgname=aufs3-util-lily-git
_gitbranch=3.9
pkgver=20141003
pkgrel=1
pkgdesc="Another Unionfs Implementation that supports NFS branches"
arch=('i686' 'x86_64')
url="http://aufs.sourceforge.net/"
license=('GPL2')
makedepends=('linux-lily-headers' 'git')
replaces=('aufs2-util')
source=("${pkgname}::git://git.code.sf.net/p/aufs/aufs-util#branch=aufs${_gitbranch}")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  date -d @$(git log -n1 --pretty=format:%ct) '+%Y%m%d'
}

prepare() {
  cd "${srcdir}/${pkgname}"
  sed -i 's/($|\\\\.x)/($|\\\\.x|-[0-9]*$)/' ver.c
}

build() {
  cd "${srcdir}/${pkgname}"
  lilyver=$(pacman -Q linux-lily-headers | awk '{print $2}')
  mkdir -p libau/linux
  cp "/lib/modules/${lilyver}-lily/build/include/uapi/linux/aufs_type.h" libau/linux
  sed -i 's/__user//g' libau/linux/aufs_type.h
  mkdir -p fhsm/libau/linux
  cp libau/linux/aufs_type.h fhsm/libau/linux
  sed -i 's/-lrt -L. -lfhsm -L.. -lautil/-L. -lfhsm -L.. -lautil -lrt/' fhsm/Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install

  #fixes for the archlinux /usr/bin move
  mv "${pkgdir}"/sbin/* "${pkgdir}"/usr/bin/
  rmdir "${pkgdir}"/sbin
}
