# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

_pkgname=libfilezilla
pkgname=$_pkgname-svn
pkgver=9011
pkgrel=1
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL')
depends=('gcc-libs' 'nettle')
makedepends=('doxygen' 'clang' 'subversion')
checkdepends=('cppunit')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::svn+https://svn.filezilla-project.org/svn/libfilezilla/trunk")
md5sums=(SKIP)

pkgver() {
    cd $_pkgname
    svnversion
}

build() {
  cd "${srcdir}/$_pkgname"
  export CXX=clang++
  export CC=clang
  autoreconf -i
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd "${srcdir}/$_pkgname"
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd "${srcdir}/$_pkgname"
  make DESTDIR="${pkgdir}" install
}

