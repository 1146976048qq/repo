# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Kyle Keen <keenerd at gmail dot com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-pyzmq
pkgname=(${_pypy3name}-pyzmq ${_pypyname}-pyzmq)
pkgver=14.3.1
pkgrel=4
pkgdesc="Python bindings for zeromq, pypy version"
arch=(i686 x86_64)
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
makedepends=(zeromq pypy-${_pypyabi} pypy3-${_pypy3abi})
# source=(https://github.com/zeromq/pyzmq/downloads/pyzmq-$pkgver.tar.gz)
source=(https://pypi.python.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz
        compiler-pypy.json
        compiler-pypy3.json
        cc)
md5sums=('7196b4a6fbf98022f17ffa924be3d68d'
         '9222c8150542cf09e89ee0a4ebac4ce7'
         '833310ff1f1bfe9ca0a4d65424142b13'
         'fbbb0d16fdb555994e28e57ff8cbaf04')
options=('debug' 'strip' '!emptydirs')

build() {
  # Clean up
  find . -d -name '__pycache__' -exec rm -vr {} \;
  find . -name '*.so' -exec rm -v {} \;
  rm -rf py2zmq-${pkgver}

  cp -a pyzmq-${pkgver} py2zmq-${pkgver}

  (cd pyzmq-$pkgver
   # py3 errors added in 2.2.0.1
   sed -i 's|except socket.error, e:|except socket.error as e:|' \
       zmq/eventloop/ioloop.py
   sed -i 's|except gevent.Timeout, t:|except gevent.Timeout as t:|' \
       zmq/green/core.py
   sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env pypy3|' \
       $(find ./ -name '*.py')
   pypy3 setup.py build)

  (cd py2zmq-$pkgver
   sed -i 's|^#!/usr/bin/env python$|#!/usr/bin/env pypy|' \
       $(find ./ -name '*.py')

   pypy setup.py build)
}

_fix_py2_cffi() {
  cd "${srcdir}"
  mv -v "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json" \
     compiler.json.bak
  cp compiler-pypy.json \
     "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json"
  __PYZMQ_PKGDIR="${pkgdir}/opt/pypy" \
                PYTHONPATH="${pkgdir}/opt/pypy/site-packages" \
                PATH="${srcdir}:${PATH}" pypy -c "import $1"
  find "$pkgdir/" -name '*.o' -delete
  find "$pkgdir/" -name '*.c' -delete
  mv -v compiler.json.bak \
     "${pkgdir}/opt/pypy/site-packages/zmq/utils/compiler.json"
}

_fix_py3_cffi() {
  pypy3_site_path=/opt/pypy3/site-packages/
  cd "${srcdir}"
  mv -v "${pkgdir}/${pypy3_site_path}/zmq/utils/compiler.json" compiler.json.bak
  cp compiler-pypy3.json "${pkgdir}/${pypy3_site_path}/zmq/utils/compiler.json"
  __PYZMQ_PKGDIR="${pkgdir}/opt/pypy3" \
                PYTHONPATH="${pkgdir}/${pypy3_site_path}" \
                PATH="${srcdir}:${PATH}" pypy3 -c "import $1"
  find "$pkgdir/" -name '*.o' -delete
  find "$pkgdir/" -name '*.c' -delete
  mv -v compiler.json.bak "${pkgdir}/${pypy3_site_path}/zmq/utils/compiler.json"
}

package_pypy-hg-pyzmq() {
  pkgdesc="PyPy bindings for zeromq"
  depends=(zeromq pypy-${_pypyabi})
  conflicts=(pypy-pyzmq)
  provides=(pypy-${_pypyabi}-pyzmq=${pkgver}-${pkgrel}
            pypy-pyzmq=${pkgver}-${pkgrel})

  cd py2zmq-$pkgver

  pypy setup.py install --skip-build --root="$pkgdir" --optimize=1
  (_fix_py2_cffi zmq)
}

package_pypy3-hg-pyzmq() {
  pkgdesc="PyPy3 bindings for zeromq"
  depends=(zeromq pypy3-${_pypy3abi})
  conflicts=(pypy3-pyzmq)
  provides=(pypy3-${_pypy3abi}-pyzmq=${pkgver}-${pkgrel}
            pypy3-pyzmq=${pkgver}-${pkgrel})

  cd pyzmq-$pkgver

  pypy3 setup.py install --skip-build --root="$pkgdir" --optimize=1
  (_fix_py3_cffi zmq)
}
