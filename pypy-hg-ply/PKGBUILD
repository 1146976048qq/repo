# Maintainer: Yichao Yu <yyc1992@gmail.com>

_pypyname=pypy-hg
_pypyabi=26
_pypy3name=pypy3-hg
_pypy3abi=25

pkgbase=${_pypyname}-ply
pkgname=(${_pypy3name}-ply ${_pypyname}-ply)
pkgver=3.4
pkgrel=4
pkgdesc='Implementation of lex and yacc parsing tools'
arch=('any')
url='http://www.dabeaz.com/ply/'
license=('BSD')
makedepends=(pypy3-${_pypy3abi}-setuptools pypy-setuptools pypy3-${_pypy3abi})
source=("${url}ply-$pkgver.tar.gz")
sha256sums=('af435f11b7bdd69da5ffbc3fecb8d70a7073ec952e101764c88720cdefb2546b')

prepare() {
  cp -r ply-$pkgver{,-py2}
}

package_pypy3-hg-ply() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-ply=${pkgver}
            pypy3-${_pypy3abi}-ply=${pkgver})
  conflicts=(pypy3-ply)
  cd ply-$pkgver

  pypy3 setup.py install --root="$pkgdir"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n30 README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pypy-hg-ply() {
  depends=(pypy)
  provides=(pypy-ply=${pkgver})
  conflicts=(pypy-ply)
  cd ply-$pkgver-py2

  pypy setup.py install --root="$pkgdir"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n30 README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
