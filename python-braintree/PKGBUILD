# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-braintree
pkgname=(python-braintree python2-braintree)
_pkgname=braintree
pkgver=3.11.0
pkgrel=1
pkgdesc="Braintree Python Library"
arch=('any')
url="https://www.braintreepayments.com/docs/python"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver::https://github.com/braintree/braintree_python/archive/${pkgver}.tar.gz")
sha512sums=('c914b70f12496d53aef873076fa0b2f100c95039cf1cc33bf49e0f6768a1805f1f4f670befe6b037d89a78b8e033a03ba533fbef39b920db1f0d723c4c5c9240')

prepare() {
  cp -a ${_pkgname}_python-$pkgver{,-py2}
}

check() {
  cd ${_pkgname}_python-$pkgver
  # https://github.com/braintree/braintree_python/issues/46https://github.com/braintree/braintree_python/issues/46
  nosetests3 tests/unit || warning "Tests failed"

  cd ../${_pkgname}_python-$pkgver-py2
  nosetests2 tests/unit
}

package_python-braintree() {
  depends=('python-requests')

  cd ${_pkgname}_python-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-braintree() {
  depends=('python2-requests')

  cd ${_pkgname}_python-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

