# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Pierre Chapuis <catwell at archlinux dot us>

_pypyname=pypy
_pypyabi=24
_pypy3name=pypy3
_pypy3abi=24

pkgbase=${_pypyname}-decorator
pkgname=(${_pypy3name}-decorator ${_pypyname}-decorator)
pkgver=3.4.0
pkgrel=6
pkgdesc='Python Decorator module'
arch=('any')
url='http://pypi.python.org/pypi/decorator'
license=('BSD')
makedepends=(pypy pypy-setuptools pypy3-${_pypy3abi}-setuptools
             pypy3-${_pypy3abi})
source=("http://pypi.python.org/packages/source/d/decorator/decorator-${pkgver}.tar.gz"
        'LICENSE.txt')
md5sums=('1e8756f719d746e2fc0dd28b41251356'
         '0ca76d2c707f09dbb04acc425ea1a08b')

build() {
  # Make python and python2 builds possible
  cp -r decorator-$pkgver decorator2-$pkgver

  (cd decorator-$pkgver
   pypy3 setup.py build)

  (cd decorator2-$pkgver
   pypy setup.py build)
}

package_pypy-decorator() {
  depends=(pypy)
  cd decorator2-${pkgver}

  pypy setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 "${srcdir}/LICENSE.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_pypy3-decorator() {
  depends=(pypy3-${_pypy3abi})
  provides=(pypy3-${_pypy3abi}-decorator=${pkgver}-${pkgrel})
  cd decorator-${pkgver}

  pypy3 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 "${srcdir}/LICENSE.txt" \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
