pkgname=sharpfont
pkgdesc="Cross-platform FreeType bindings for .NET"
pkgver=2.5.0.1
pkgrel=1
arch=(any)
license=("MIT")
url="https://github.com/Robmaister/SharpFont"
source=("https://github.com/Robmaister/SharpFont/archive/v$pkgver.tar.gz"
"sharpfont.pc.in")
depends=(mono freetype2)
sha1sums=('497a3bd4d8f23fd95ba8daf3e342271869009b33'
          '06f0b1549c5ecb11fef7a40e77e9a4c90cf1e3c7')

prepare() {
  cd "${srcdir}/SharpFont-$pkgver/SharpFont/TrueType"
  mv EncodingID.cs EncodingId.cs
  mv PCLT.cs Pclt.cs
  mv PlatformID.cs PlatformId.cs
}
          
build() {
	cd "${srcdir}/SharpFont-$pkgver"
	xbuild SharpFont.sln /p:Configuration=Release
}

package() {
	cd "${srcdir}/SharpFont-$pkgver"
	install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	cd SharpFont/bin/Release
	find . -name 'SharpFont.dll*' -exec install -Dm644 {} "$pkgdir/usr/lib/sharpfont/"{} \;
	find "$pkgdir" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	install -Dm644 "$srcdir/sharpfont.pc.in" "$pkgdir/usr/lib/pkgconfig/sharpfont.pc"
	sed -i "s,@VERSION@,$pkgver," "$pkgdir/usr/lib/pkgconfig/sharpfont.pc"
}
