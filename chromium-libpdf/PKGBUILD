# Maintainer: Andrew Webley <unsuspectinghero@gmail.com>
# Contributor: Dmitriy Morozov <archlinux@foxcub.org>

pkgname="chromium-libpdf"
#The pkgver is the same as for google chrome dev. The library isn't meant to be distributed on its own so no other version information exists.
pkgver="33.0.1750.5"
pkgrel="1"
pkgdesc="A PDF Viewer for Chromium and Chromium based browsers"
url="http://wiki.archlinux.org/index.php/Chromium#libpdf.so"
arch=("i686" "x86_64")
license=("custom:chrome")
depends=("gcc-libs")
optdepends=("chromium: default browser to use plugin in (one of the optional dependencies needs to be installed to use the library)"
            "chromium-dev: development version of default browser, requires modifying the PKGBUILD"
            "iron-bin: alternate google-chrome based browser, requires modifying the PKGBUILD")
sha512sums=("395118e45e8c43cf491c48d932c9c49d056153d52d961d2812169b39256c398b6f16f37e2124ebf1daff31a625006f92225c537547b2e11605ce132ea4e2fa81")
source=("LICENSE")
if [ "$CARCH" = "i686" ]; then
  source+=("https://dl.google.com/linux/direct/google-chrome-unstable_current_i386.rpm")
  sha512sums+=("c7cd66254eef98c9407399bc9c578388a8f905a94fb6fefb57758480db4d0d47aa6755d9f2b3b42dc8f07a08c4b7eb6771ad41658f7c9ec49bc300d08427e302")
elif [ "$CARCH" = "x86_64" ]; then
  source+=("https://dl.google.com/linux/direct/google-chrome-unstable_current_x86_64.rpm")
  sha512sums+=("902b7525471f490a18f8d83f3c04c3b28a8cff11d4d5f1c384bdc6660be99aebf2cfb9f587ac78341c8d89d8042a67197c23cee3677556574835bf4742a82f59")
fi

package(){
	#Uncomment the relevant line below to install the library in the correct place if you're running something other than chromium.

  #chromium
  install -Dm755 "$srcdir/opt/google/chrome/libpdf.so" "$pkgdir/usr/lib/chromium/libpdf.so"

  #chromium-dev
  #install -Dm755 "$srcdir/opt/google/chrome/libpdf.so" "$pkgdir/usr/lib/chromium-dev/libpdf.so"

  #iron-browser
  #install -Dm755 "$srcdir/opt/google/chrome/libpdf.so" "$pkgdir/opt/iron/libpdf.so"

  install -Dm755 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
