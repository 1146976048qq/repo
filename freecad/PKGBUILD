# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad-oce
pkgver=0.17
_commit=9948ee4
pkgrel=8
pkgdesc='A general purpose 3D CAD modeler'
arch=('x86_64')
url='http://www.freecadweb.org/'
license=('LGPL')
depends=('curl' 'oce' 'xerces-c' 'libspnav' 'glu' 'netcdf'
         'shared-mime-info' 'hicolor-icon-theme' 'jsoncpp' 'qt5-base' 'qt5-declarative' 'qt5-svg' 'qt5-tools'
         'med' 'python2-pivy' 'python2-pyside2' 'python2-matplotlib' 'pyside2-tools')
makedepends=('boost' 'eigen' 'gcc-fortran' 'swig' 'xerces-c' 'desktop-file-utils' 'git'
             'cmake' 'coin>=3.1.3-9' 'python2-pyside2' 'python2-shiboken2' 'pyside2' 'shiboken2' vtk)
optdepends=('python2-matplotlib' 'python2-pyqt5' 'graphviz' 'openscad')
options=(!libtool !staticlibs debug strip)
optdepends=('python2-matplotlib' 'python2-pyqt5' 'graphviz' 'openscad')
source=("git+https://github.com/FreeCAD/FreeCAD.git#commit=$_commit"
        "freecad.desktop" "freecad.xml"
        'gcc8.patch' 'smesh-pthread.patch' 'qt5.11.patch')
sha256sums=('SKIP'
            '617968d7bbd1da71bdedaed1b66c5d6eaf24e0fb34678b93f5d925d370c66296'
            '248918de7d3c2145b5cc4fbbc9e224d22f4a6ca7ead2680e8c3a32e91772482a'
            '618bb85c4f3a4eb0e329d1fc30391b777c9b0cffe97aa1e96d45f58b18424311'
            '170c90ee6ef64cf3c8e6a35ca94bb1187d346707f7e0779022e614563c6b74f6'
            'a639c9d51f8443e4d2270fe60d5ac3ac62c7c64c532620108514840f8e8704bc')

prepare() {
  cd "${srcdir}/FreeCAD"

  patch -Np1 -i ../gcc8.patch
  patch -Np1 -i ../smesh-pthread.patch
  patch -Np1 -i ../qt5.11.patch
}

build() {
  cd "${srcdir}/FreeCAD"
  mkdir -p build
  cd build

  cmake .. -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr/lib/freecad \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib/freecad \
    -DCMAKE_INSTALL_DOCDIR=/usr/share/freecad/doc \
    -DCMAKE_INSTALL_DATADIR=/usr/share/freecad \
    -DCMAKE_INSTALL_INCLUDEDIR=/usr/include \
    -DOCC_INCLUDE_DIR=/usr/include/oce/ \
    -DFREECAD_USE_OCC_VARIANT="Official Version" \
    -DFREECAD_USE_EXTERNAL_PIVY=ON \
    -DBUILD_QT5=ON \
    -DBUILD_QT5_WEBKIT=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DBUILD_START=OFF \
    -DBUILD_WEB=OFF \
    -DOCE_DIR=/usr/lib/oce-0.18-dev/
  make
}

package_freecad-oce() {
  cd FreeCAD/build
  provides=('freecad')
  conflicts=('freecad')

  # Install main program
  make DESTDIR="${pkgdir}" install

  # Symlink to /usr/bin
  mkdir -p "${pkgdir}/usr/bin"
  ln -sf "../lib/freecad/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
  ln -sf "../lib/freecad/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"

  # Install pixmaps and desktop shortcut
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/freecad.desktop"
  for i in 16 32 48 64; do
    install -Dm644 "../src/Gui/Icons/freecad-icon-${i}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/freecad.png"
  done
  install -Dm644 "../src/Gui/Icons/freecad.svg" \
          "${pkgdir}/usr/share/icons/hicolor/scalable/apps/freecad.svg"

  # Mime info
  install -D -m644 "${srcdir}/freecad.xml" \
          "${pkgdir}/usr/share/mime/packages/freecad.xml"

  boostver=$(objdump -p /usr/lib/libboost_system.so | \
               grep SONAME | sed -e 's/.*boost_system\.so\.//')
  [[ $boostver =~ ^([0-9]*)\.([0-9]*)\.([0-9]*) ]]
  maj_ver=${BASH_REMATCH[1]}
  min_ver=${BASH_REMATCH[2]}
  patch_ver=${BASH_REMATCH[3]}
  ((next_min=min_ver + 1))
  depends+=("boost-libs>=${maj_ver}.${min_ver}.${patch_ver}"
            "boost-libs<${maj_ver}.${next_min}.0")
}
