# Maintainer: Arthur Zamarin <arthurzam@gmail.com>

pkgname=openlibm-git
pkgver=0.5.0.6.g5501bf0
pkgrel=1
pkgdesc="standalone implementation of C mathematical functions"
arch=(i686 x86_64 armv7h aarch64)
url="https://github.com/JuliaLang/openlibm"
license=('BSD')
source=("git://github.com/JuliaLang/openlibm")
sha256sums=('SKIP')
provides=('openlibm=0.4.1')
conflicts=('openlibm')
options=('debug' 'strip')
makedepends=('git')

pkgver() {
  cd openlibm

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd openlibm
  git reset --hard HEAD
  git clean -fdx

  sed -i "s@/usr/local@/usr@" Make.inc
}

build() {
  cd openlibm
  make
}

package() {
  cd openlibm
  make DESTDIR="$pkgdir" install
}
