# Maintainer: Arthur Zamarin <arthurzam@gmail.com>

pkgbase=openlibm-git
if [[ $CARCH = x86_64 ]]; then
    pkgname=(openlibm-git openlibm-git-avx2)
else
    pkgname=(openlibm-git)
fi
pkgver=0.5.5
pkgrel=2
pkgdesc="standalone implementation of C mathematical functions"
arch=(i686 x86_64 armv7h aarch64)
url="https://github.com/JuliaLang/openlibm"
license=('BSD')
provides=('openlibm=0.5.4')
conflicts=('openlibm')
options=('debug' '!strip')
makedepends=('git')
source=("git://github.com/JuliaLang/openlibm"
        0001-Fix-compilation-on-AArch64.patch)
md5sums=('SKIP'
         '075e8933e266329e339ffd97aab68b8a')

_CFLAGS_common=' -g -flto -O3'
_LDFLAGS_common=' -g -flto -O3 -fno-plt -Wl,-Bsymbolic-functions'

pkgver() {
  cd openlibm

  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  (cd openlibm
   git reset --hard HEAD
   git clean -fdx

   patch -Np1 < ../0001-Fix-compilation-on-AArch64.patch

   sed -i "s@/usr/local@/usr@" Make.inc)

  if [[ $CARCH = x86_64 ]]; then
      rm -rf openlibm-avx2
      cp -a openlibm openlibm-avx2
  fi
}

build() {
  CFLAGS+="$_CFLAGS_common"
  LDFLAGS+="$_LDFLAGS_common"
  (cd openlibm
   make)

  if [[ $CARCH = x86_64 ]]; then
      (cd openlibm-avx2
       CFLAGS+=' -mavx2 -march=haswell'
       LDFLAGS+=' -mavx2 -march=haswell'
       make)
  fi
}

package_openlibm-git() {
  cd openlibm
  provides+=('openlibm-git-debug')
  replaces+=('openlibm-git-debug')
  (CFLAGS+="$_CFLAGS_common"
   LDFLAGS+="$_LDFLAGS_common"
   make DESTDIR="$pkgdir" install)
}

package_openlibm-git-avx2() {
  cd openlibm-avx2
  provides+=(openlibm-git openlibm-avx2)
  (CFLAGS+="$_CFLAGS_common -mavx2 -march=haswell"
   LDFLAGS+="$_LDFLAGS_common -mavx2 -march=haswell"
   make DESTDIR="$pkgdir" install)
}
