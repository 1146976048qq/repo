# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org> 
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: SÃ¸ren Holm <sgh@sgh.dk> 

pkgname=vym-dev
pkgver=2.3.22
pkgrel=1
pkgdesc="A mindmapping tool, dev version."
arch=('i686' 'x86_64')
url="http://www.insilmaril.de/vym/"
license=('GPL')
depends=('qt4' 'unzip' 'zip')
options=('!emptydirs')
changelog=vym.changelog
install=vym.install
conflicts=('vym')
provides=("vym=$pkgver")
source=(http://downloads.sourceforge.net/project/vym/Development/vym-$pkgver.tar.bz2 \
        vym.desktop)

build() {
  cd "${srcdir}"/vym-$pkgver

  qmake-qt4 PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/vym-$pkgver

# install files
  make INSTALL_ROOT="${pkgdir}" install

# correct doc location
  install -d "${pkgdir}"/usr/share/{doc/vym,man/man1}
  install -m644 doc/* "${pkgdir}"/usr/share/doc/vym
  rm -rf "${pkgdir}"/usr/share/doc/packages
  mv "${pkgdir}"/usr/share/doc/vym/vym.1.gz "${pkgdir}"/usr/share/man/man1/

# .desktop and icon file
  install -D -m644 "${srcdir}"/vym.desktop \
    "${pkgdir}"/usr/share/applications/vym.desktop
  install -D -m644 "${pkgdir}"/usr/share/vym/icons/vym.png \
    "${pkgdir}"/usr/share/pixmaps/vym.png
}

sha256sums=('92bd267fc2aa404e0f0887e780bae422be922c20d8037d743f32b16a4f8a16df'
            'e299c69c213e7aac3f5b5d0ab088132b4ec7cb63a391f272e75ed64f049d541b')
