# Maintainer: Ariel AxionL <axionl@aosc.io>

pkgname=color-picker-git
pkgver=r93.f192bad
pkgrel=1
pkgdesc="A color picker for elementary OS."
arch=('x86_64')
depends=('granite')
makedepends=('meson' 'vala' 'git')
conflicts=("color-picker")
provides=("color-picker")
url="https://github.com/RonnyDo/ColorPicker"
license=("GPL3")
source=("$pkgname::git+https://github.com/RonnyDo/ColorPicker"
        "https://raw.githubusercontent.com/RonnyDo/ColorPicker/master/LICENSE")

sha256sums=('SKIP'
            '589ed823e9a84c56feb95ac58e7cf384626b9cbf4fda2a907bc36e103de1bad2')

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd $srcdir/$pkgname
    meson _build --prefix=/usr
    cd _build
    ninja
}

package() {
    install -Dm755 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
    cd $srcdir/$pkgname/_build
    DESTDIR="$pkgdir/" ninja install
}
