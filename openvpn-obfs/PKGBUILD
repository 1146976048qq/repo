# Maintainer: Sascha Weaver <wzyboies@gmail.com>

pkgname=openvpn-obfs
_pkgname=openvpn
pkgver=2.3.5
pkgrel=1
pkgdesc="OpenVPN binary with obfs patch"
arch=('i686' 'x86_64')
url="https://www.gsea.com.cn/blog/topic/traffic-obsfucate-patch-for-openvpn/"
depends=('openssl' 'lzo' 'iproute2' 'openvpn>=2.3.0' 'libsystemd')
makedepends=('systemd')
license=('custom')
source=(http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz
        OpenVPN-2.3.0-obfs.patch
        openvpn-obfs@.service)
sha256sums=('d93b447b014a41ec1550c3b760871aa72d7c09f1b5c6fc1e013d3073ac29ca21'
            'a286c581758628d0b2195c4d6121cde71b25494b9924364e0a2e2222216c20ab'
            '0f9830b732160523713cd03be3201ba3c5a5bad79fe8bbedc36776558a9feb90')
options=(!libtool)

build() {
  cd $srcdir/$_pkgname-$pkgver
  patch -p0 -i $srcdir/OpenVPN-2.3.0-obfs.patch
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-password-save \
    --mandir=/usr/share/man \
    --enable-iproute2 \
    --enable-systemd
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mv $pkgdir/usr/bin/openvpn{,-obfs}
  rm -r $pkgdir/usr/{share,lib,include}
  cd ..
  install -Dm644 openvpn-obfs@.service \
  $pkgdir/usr/lib/systemd/system/openvpn-obfs@.service
}
