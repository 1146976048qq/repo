# Maintainer: Dct Mei <dctxmei@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ariel AxionL <i@axionl.me>
# Contributor: Luke Yue <lukedyue@gmail.com>

pkgname=clash-dctxmei
_pkgname=clash
pkgver=1.1.0
pkgrel=4
pkgdesc="A rule-based tunnel in Go"
arch=('x86_64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
depends=('clash-geoip' 'glibc')
makedepends=('go')
provides=("clash=${pkgver}")
conflicts=('clash')
backup=("etc/clash/config.yaml")
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "config.yaml"
        "clash.sysusers"
        "clash.tmpfiles"
        "clash.service"
        "clash@.service")
md5sums=('82e0279d23d67628b0a30f510c944d69'
         '6c517b62529e4d8890753922c1fa25f1'
         '8bddb4b9711be51b6324d81ed30c2115'
         'c7207134abc1e99376b0645b0f93a269'
         'f0aa4d2ca141f44bf297bfd3e64ab83f'
         'a7bd3e32171f26a68498cf5890b4155f')
sha1sums=('c4e34df6960579a65e2a9269d48df8c4f01e1386'
          '6dea4739c49700262a1b3edf670e6777f3fdb645'
          '374426626729f302af503ef4efafb2badebf2b96'
          '46548ccd4b7e5449287325115b1e9d24ccfcd541'
          '670d7f5e45ef145a8d7af8e83a73a867b19e4b2b'
          '638983329eb64aac8ed2e68b71e9c06f76c1110b')
sha256sums=('00c19e27981dd0d6e9235d588a6e913d569157879b150da1f596682f5e525cae'
            '62ed4460cd2ed4b400193ad04b0cccb76d7558f87c377a0033041841a73f7945'
            '149c6448a5630af1065ea230707331ac12663128568d6cf0e9d5480e94d1d104'
            '006bea79c75de78dcd4f3991bb9c4e6f706443131aeeccf8db076f8738f24ccd'
            '090e1598e9e9736c951b1e2488df7e573c4d29d2fd0e0da8cfc0edd998f8c8fb'
            'd22cc741edf783c6fc83bb62f67b5381a0421d2ea49959469c1b8da48488a827')

prepare() {
    cd "${srcdir}"/"${_pkgname}-${pkgver}"/
    sed "s/unknown version/${pkgver}/" -i constant/version.go
    sed "s/unknown time/$(LANG=C date -u)/" -i constant/version.go
}

build() {
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    cd "${_pkgname}-${pkgver}/"
    go build -ldflags="-linkmode=external"
}

check() {
    cd "${_pkgname}-${pkgver}/"
    go test github.com/Dreamacro/clash/...
}

package() {
    cd "${_pkgname}-${pkgver}/"
    install -Dm 755 clash -t "${pkgdir}"/usr/bin/
    install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/clash/
    install -Dm 644 "${srcdir}"/config.yaml -t "${pkgdir}"/etc/clash/
    install -Dm 644 "${srcdir}"/clash.sysusers "${pkgdir}"/usr/lib/sysusers.d/clash.conf
    install -Dm 644 "${srcdir}"/clash.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/clash.conf
    install -Dm 644 "${srcdir}"/clash.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm 644 "${srcdir}"/clash@.service -t "${pkgdir}"/usr/lib/systemd/system/
}
