# Maintainer: Tom Hale <tom[noodle]hale[point]ee>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=nixnote2
pkgver=2.1.6
pkgrel=1
pkgdesc='Evernote clone (formerly Nevernote)'
url="https://github.com/robert7/$pkgname"
arch=(x86_64)
license=(GPL3)
depends=(hunspell java-runtime hicolor-icon-theme poppler-qt5 tidy qt5-webkit)
makedepends=(boost gcc git qt5-tools)
conflicts=(nixnote2-git)
replaces=(nevernote nixnote nixnote-beta)
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('ce33fb6349162d44cd9cd87af44e9b352673a183fba5afbeb946f30f01cbb6fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/tidy\/tidy.h/tidy.h/' src/main.cpp
  sed -i 's/tidy\/tidybuffio.h/tidybuffio.h/' src/main.cpp
  sed -i 's/tidy\/tidy.h/tidy.h/' src/html/enmlformatter.cpp
  sed -i 's/tidy\/tidybuffio.h/tidybuffio.h/' src/html/enmlformatter.cpp

  qmake PREFIX=/usr
  make -j $(( 1 + ($(nproc)-1) / 2 ))  # Use ceil(#processors / 2) jobs
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname}/" CONTRIBUTING.md debian/changelog
  # shortcuts.txt is referenced in doc:
  # https://github.com/robert7/nixnote2/wiki/HowTo---Customize-keyboard-shortcuts
  install -Dm644 -t "${pkgdir}/usr/share/${pkgname}/" shortcuts.txt
}
