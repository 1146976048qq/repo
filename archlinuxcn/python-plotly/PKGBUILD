# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: Grey Christoforo <first name at last name dot net>

_pkgname=plotly.py
pkgname=python-plotly
pkgver=4.14.0
pkgrel=1
pkgdesc='The interactive graphing library for Python (includes Plotly Express)'
url='https://plotly.com/python'
depends=(
  python-matplotlib
  python-numpy
  python-pandas
  python-retrying
  python-six
)
optdepends=(
  'ipython: ipython support'
  'python-jupyter: jupyter support'
  'python-ipykernel: jupyter support'
  'python-pillow: image uri conversion'
)
makedepends=(
  python-setuptools
)
license=('MIT')
arch=('any')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/plotly/plotly.py/archive/v${pkgver}.tar.gz")
sha512sums=('c56f8245595bb0fd1e0ea5bcf0878a6960f47e44c04e43999adf31ea8b3c0a9dbca600ae2e65a47a753f3c7058802157c65757c73087741b527419af89d1136b')

build() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}/packages/python/plotly
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
