_name=httpx
pkgname=python-httpx-latest
pkgver=0.14.3
pkgrel=1
pkgdesc="The next generation HTTP client."
arch=(any)
url="https://github.com/encode/httpx"
license=('BSD')
depends=('python-hstspreload' 'python-sniffio' 'python-chardet' 'python-idna' 'python-rfc3986' 'python-httpcore-latest')
makedepends=('python-setuptools')
optdepends=('python-brotlipy: decoding for "brotli" compressed responses' 'python-urllib3: support for the httpx.URLLib3Transport class')
conflicts=('python-httpx')
provides=('python-httpx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/httpx-0.14.3.tar.gz")
sha256sums=('96bd4de4e6b742d672e2338720baf98518efaf85c86e0b48218e1bef9f272333')

prepare() {
  cd "$srcdir/httpx-0.14.3"
  sed -i '/certifi/d' setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i httpx/_config.py
}

build() {
  cd "$srcdir/httpx-0.14.3"
  python3 setup.py build
}

package() {
  cd "$srcdir/httpx-0.14.3"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

