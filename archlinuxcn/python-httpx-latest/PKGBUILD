_name=httpx
pkgname=python-httpx-latest
pkgver=0.16.0
pkgrel=1
pkgdesc="The next generation HTTP client."
arch=(any)
url="https://github.com/encode/httpx"
license=('BSD')
depends=('python-hstspreload' 'python-sniffio' 'python-chardet' 'python-idna' 'python-rfc3986' 'python-httpcore-latest')
makedepends=('python-setuptools')
optdepends=('python-brotlipy: decoding for "brotli" compressed responses' 'python-urllib3: support for the httpx.URLLib3Transport class')
conflicts=('python-httpx')
provides=('python-httpx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/httpx-0.16.0.tar.gz")
sha256sums=('032c624c703cfa3aafe2d3a766150b48bcf8df71e833c1440fa74151da9df143')

prepare() {
  cd "$srcdir/httpx-0.16.0"
  sed -i '/certifi/d' setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i httpx/_config.py
}

build() {
  cd "$srcdir/httpx-0.16.0"
  python3 setup.py build
}

package() {
  cd "$srcdir/httpx-0.16.0"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

