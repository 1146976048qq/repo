# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=zfp
pkgname=(zfp zfp-cuda)
pkgver=0.5.5
pkgrel=2
pkgdesc='Library for compressed numerical arrays that support high throughput read and write random access'
arch=('x86_64')
url='http://computing.llnl.gov/projects/floating-point-compression'
license=('BSD')
depends=('gcc-libs')
makedepends=(
  'cmake'
  'cuda'
  'cython'
  'gcc-fortran'
  'python-numpy'
)
optdepends=(
  'python-numpy: python binding'
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/LLNL/zfp/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('fdf7b948bab1f4e5dccfe2c2048fd98c24e417ad8fb8a51ed3463d04147393c5')

prepare() {
  mkdir "${srcdir}/${_pkgname}-${pkgver}/build"
  cp -a "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/${_pkgname}-cuda-${pkgver}"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  cmake .. \
		-DBUILD_ALL:BOOL=ON \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
		-DZFP_BUILD_TESTING_LARGE:BOOL=ON \
		-DZFP_WITH_CUDA:BOOL=OFF
  make
  
  cd "${srcdir}/${_pkgname}-cuda-${pkgver}/build"
  cmake .. \
		-DBUILD_ALL:BOOL=ON \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
		-DZFP_BUILD_TESTING_LARGE:BOOL=ON \
		-DZFP_WITH_CUDA:BOOL=ON 
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  ctest -v
  
  cd "${srcdir}/${_pkgname}-cuda-${pkgver}/build"
  ctest -v
}

_package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_zfp() {
  _package
}

package_zfp-cuda() {
  pkgdesc="${pkgdesc} (with CUDA)"
  depends+=(cuda)
  provides=(zfp=${pkgver})
  conflicts=(zfp)

  _package
}
# vim:set ts=2 sw=2 et:

