# Maintainer: Iru Cai <mytbk920423@gmail.com>
# Contributor: Alexander Hunziker <alex.hunziker@gmail.com>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=gegl-git
_pkgname=gegl
pkgver=0.4.16.260
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('i686' 'x86_64')
url="http://www.gegl.org"
license=('GPL3' 'LGPL3')
depends=('babl-git' 'libspiro' 'poppler-glib' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'luajit' 'jasper' 'lensfun' 'suitesparse' 'sdl')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'enscript' 'asciidoc' 'vala' 'gobject-introspection' 'git')
optdepends=('ffmpeg: ffmpeg plugin')
provides=("gegl=${pkgver}")
conflicts=('gegl>=0.3.0')
options=(!libtool)
source=(git+https://gitlab.gnome.org/GNOME/gegl.git)
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always | sed -e 's/GEGL_//' -e 's/-g.*$//' -e 's/[_-]/./g'
}

build() {
  #mkdir path
  #ln -s /usr/bin/python2 path/python

  #export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${_pkgname}
  NOCONFIGURE=1 ./autogen.sh
  ./configure  --prefix=/usr  --with-sdl --with-librsvg \
    --with-libavformat --with-jasper \
    --enable-workshop

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  #export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${_pkgname}
  make -k check || :
}

package() {
  #export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${_pkgname}
  make DESTDIR="${pkgdir}" install
}
