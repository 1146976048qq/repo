# First Linux Arch packager: Serge Victor <arch@random.re>

_pkgname=addict
pkgname=python-addict
pkgver=2.3.0
pkgrel=1
pkgdesc="the Python Dict that's better than heroin"
arch=('any')
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
url='https://github.com/mewwts/addict'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mewwts/addict/archive/v${pkgver}.tar.gz")
sha512sums=('7c3a90f57c744d9b79e91023293c8b7d9edf7c31429f636118486d9279e2a09d4485ce7db3297ed26d26591d2423f46d0b7b717dffe4283f432506b10806af89')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
