# Maintainer: Dobroslaw Kijowski [dobo] <dobo90_at_gmail.com>

pkgname=lief
pkgver=0.10.1
pkgrel=3
pkgdesc='Library to instrument executable formats'
arch=(x86_64)
url='https://lief.quarkslab.com/'
license=(apache)
depends=(python)
makedepends=(git cmake python-setuptools)
source=("git+https://github.com/lief-project/LIEF#tag=${pkgver}")
md5sums=(SKIP)

build() {
  cd "${srcdir}/LIEF"
  mkdir build

  cmake . -B build -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=1
  make -C build
  python setup.py build --build-temp=build
}

package() {
  cd "${srcdir}/LIEF"

  make -C build DESTDIR="${pkgdir}" install
  python setup.py install --optimize=1 --root="${pkgdir}" --skip-build

  if [[ -f "$pkgdir/usr/include/json.hpp" ]]; then
    mv "$pkgdir/usr/include/json.hpp" "$pkgdir/usr/include/LIEF/json.hpp"
    cat >> "$pkgdir/usr/include/LIEF/json.hpp" <<EOF

#ifndef LIEF_JSON_H_
#define LIEF_JSON_H_

#include <LIEF/config.h>

using json = nlohmann::json;

#endif
EOF
  fi
}
