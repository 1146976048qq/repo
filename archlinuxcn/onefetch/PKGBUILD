# Maintainer: KingofToasters <themanhimself at sgregoratto dot me>
# Contributor: Ariel AxionL <i (at) axionl (dot) me>

pkgname=onefetch
pkgver=1.6.0
pkgrel=1
depends=('zlib' 'gcc-libs')
makedepends=('rust' 'cargo')
url="https://github.com/o2sh/onefetch"
pkgdesc="Displays information about your project directly on your terminal"
license=('MIT')
source=("$url/archive/v$pkgver.tar.gz")
arch=('x86_64')
sha256sums=('63dfda9e8fcefb884d7311334590bbcc4ede5e6ff18ef1ccd4b5598071beea79')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --release
}

package() {
    install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm755 "$srcdir/$pkgname-$pkgver/target/release/$pkgname" "$pkgdir/usr/bin/"
    install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/"
}
