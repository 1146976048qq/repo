# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
pkgver=1.4.1
pkgrel=2
pkgdesc="An implementation of tldr in Rust"
arch=('i686' 'x86_64')
url="https://github.com/dbrgn/tealdeer"
license=('MIT' 'Apache')
makedepends=('rust' 'cargo')
provides=('tldr')
conflicts=('tldr' 'nodejs-tldr' 'nodejs-tldr-git' 'tldr-cpp-client' 'tldr-git' 'tldr-python-client')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dbrgn/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('eaf42fe17be751985cbf46c170ef623fcbd36028c88c2e70823492a9335a4a8e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cargo build --release
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -D target/release/tldr -t "${pkgdir}/usr/bin"
  install -Dm644 bash_tealdeer "${pkgdir}/usr/share/bash-completion/completions/tldr"
  install -Dm644 fish_tealdeer "${pkgdir}/usr/share/fish/completions/tldr.fish"
  install -Dm644 zsh_tealdeer "${pkgdir}/usr/share/zsh/site-functions/_tldr"
}

# vim:set ts=2 sw=2 et:
