# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=ITKPythonPackage
pkgname=python-itk
pkgver=5.1.1
pkgrel=2
pkgdesc='An open-source toolkit for multidimensional image analysis. This package provides only the python binding'
arch=('x86_64')
url='https://itk.org/'
license=('Apache')
depends=(
  gcc-libs
  python-numpy
)
makedepends=(
  clang
  doxygen
  git
  python-setuptools
  python-scikit-build
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/InsightSoftwareConsortium/ITKPythonPackage/archive/v${pkgver}.tar.gz")
sha512sums=('e7d6b65c508795fc032f1cadf30abb0b208c775b850ad1b64f4baa790c09f7a676a955761c3296714a214925cacd33043a074cd4abf52909c35531a5246ce683')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
  export CC=clang
  export CXX=clang++
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  mv "${pkgdir}/usr/itkHelpers.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/itkHelpers.py"
}
# vim:set ts=2 sw=2 et:

