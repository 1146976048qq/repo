# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.5
pkgrel=2
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=(
  'charls'
  'libxml2'
  'poppler'
  'python'
  'vtk'
)
makedepends=(
  'cmake'
  'doxygen'
  'docbook-xsl'
  'git'
  'glew'
  'graphviz'
  'hdf5'
  'libxslt'
  'netcdf'
  'openmpi'
  'proj'
  'qt5-base'
  'qt5-multimedia'
  'swig'
  'texlive-latexextra'
  'utf8cpp'
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
  "01-fix-building-with-poppler.patch"
)
sha512sums=('da3453230e9fe0890e750cc10b74a7e07a5efd916219118af260b3710e15f0972239df746091e9c639e65201126c91c881b4ad3b8a836681993f8294d1cad187'
            '3a8f825d07a687f211874d99237ee666280203e06b7c65fb152be1ed103d2ff04e255817c21da36fc8cebacda6faa8a12525fa60decb3c33d858f3342b29bd0a')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/01-fix-building-with-poppler.patch"
}

build() {
  export CXXFLAGS+=" ${CPPFLAGS} -fpermissive"
  cmake \
    -B build \
    -S "${_pkgname}-${pkgver}" \
    -DCHARLS_LIBRARY=/usr/lib/libcharls.so \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=OFF \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=ON \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_WRAP_PYTHON:BOOL=ON
  make -C "build"
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install

  # fix python binding installation
  install -d "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  ln -s "/usr/lib/_gdcmswig.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/_gdcmswig.so"
  ln -s "/usr/lib/vtkgdcmPython.so" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/vtkgdcmPython.so"
  mv -v "${pkgdir}/usr/lib/gdcm.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  mv -v "${pkgdir}/usr/lib/vtkgdcm.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
  mv -v "${pkgdir}/usr/lib/gdcmswig.py" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
}
# vim:set ts=2 sw=2 et:

