# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Chris <christopher.r.mullins g-mail>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=gdcm
_pkgname=GDCM
pkgver=3.0.4
pkgrel=3
pkgdesc='A C++ library for DICOM medical files'
arch=('x86_64')
url='http://gdcm.sourceforge.net'
license=('BSD')
depends=(
  'charls'
  'libxml2'
  'poppler'
  'python'
  'vtk'
)
makedepends=(
  'cmake'
  'doxygen'
  'git'
  'glew'
  'graphviz'
  'hdf5'
  'libxslt'
  'netcdf'
  'openmpi'
  'proj'
  'qt5-base'
  'qt5-multimedia'
  'swig'
  'texlive-latexextra'
)

source=(
  "https://github.com/malaterre/GDCM/archive/v${pkgver}.tar.gz"
)
sha512sums=('ee65e12b431a61ac6d9564e4c87c9a2cc8d28fe2c6bac5a9db10eeab29851593fa02d5b16d618ca4887fefbb43e23aac24efd013125df2890abaddc892108fde')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
    -DGDCM_BUILD_APPLICATIONS:BOOL=ON \
    -DGDCM_BUILD_DOCBOOK_MANPAGES:BOOL=ON \
    -DGDCM_BUILD_EXAMPLES:BOOL=OFF \
    -DGDCM_BUILD_SHARED_LIBS:BOOL=ON \
    -DGDCM_BUILD_TESTING:BOOL=OFF \
    -DGDCM_DOCUMENTATION:BOOL=ON \
    -DGDCM_LEGACY_REMOVE:BOOL=ON \
    -DGDCM_USE_SYSTEM_CHARLS:BOOL=ON \
    -DGDCM_USE_SYSTEM_EXPAT:BOOL=ON \
    -DGDCM_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENJPEG:BOOL=ON \
    -DGDCM_USE_SYSTEM_OPENSSL:BOOL=ON \
    -DGDCM_USE_SYSTEM_POPPLER:BOOL=ON \
    -DGDCM_USE_SYSTEM_UUID:BOOL=ON \
    -DGDCM_USE_SYSTEM_ZLIB:BOOL=ON \
    -DGDCM_USE_VTK:BOOL=ON \
    -DGDCMV2_0_COMPATIBILITY:BOOL=OFF \
    -DGDCM_WRAP_PYTHON:BOOL=ON \
    ..
  make -j1
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm644 Copyright.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  # fix python binding installation
  install -d "${pkgdir}/usr/lib/python3.8/site-packages"
  mv -v "${pkgdir}/usr/lib/_gdcmswig.so" "${pkgdir}/usr/lib/python3.8/site-packages"
  find "${pkgdir}/usr/lib" -type f -name 'py$' -exec mv -v {} "${pkgdir}/usr/lib/python3.8/site-packages" \;
}
# vim:set ts=2 sw=2 et:

