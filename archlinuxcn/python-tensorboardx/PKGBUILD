# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=tensorboardX
pkgname=python-tensorboardx
pkgver=1.2
pkgrel=1
pkgdesc='tensorboard for pytorch (and chainer, mxnet, numpy, ...)'
arch=(any)
url=https://github.com/lanpa/tensorboardX
license=(MIT)
depends=(python-numpy python-protobuf python-six)
makedepends=(python-setuptools)
checkdepends=(flake8 python-future python-matplotlib python-pytest python-pytorch python-torchvision)
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/lanpa/tensorboardX/archive/v${pkgver}.tar.gz")
sha512sums=('df07d3d4f9e00af75d5fd54e6523ef822267c671c3e9f979dad17ba53fe192a953a97114d378a26d9b7f42c84887a16e2259d6b900b4a820806593f483b01314')

prepare() {
  cd "${_pkgname}-${pkgver}"
  _ver=$(protoc --version|cut -d ' ' -f 2)
  # using protobuf from official repo
  sed -i "s/3.6.1/${_ver}/" compile.sh
}


build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

#check() {
#  cd "${_pkgname}-${pkgver}"
#  pytest -v
#}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
