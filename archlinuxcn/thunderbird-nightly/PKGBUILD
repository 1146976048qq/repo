# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=thunderbird
_channel=nightly
pkgbase=thunderbird-nightly
pkgname=()
_version=67.0a1
pkgver=67.0a1.20190225.10
pkgrel=1
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig'
         'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg'
         'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss'
         'shared-mime-info' 'sqlite' 'startup-notification')
_url="https://ftp.mozilla.org/pub/${_name}/nightly/latest-comm-central-l10n"
_filename_prefix="20190225.10-"
source=("${pkgbase}.desktop")
sha512sums=('ef9cf2a1aecda97587095c2096e6e7d27840da80ec0071341421cbb4bf4d2e06a01b5c95dac3f3c8ea520700035328f8e6becffa0243c1a8da8c6c710126d5a4'
            'cddd11ed06542ce50edb3173d280e6f3d093226989759f19f6dd79a46c953003ef70d6b05406232aec87276bba95a1cb11abb250f83cac016164b4fd4ea34c44'
            'SKIP'
            'e62659ae541415bf83419668b711b8000bac10036147c91297541116f469a00b58e8b9ac42c42857198efbe80891d32c9f8b7cea5d1a9ff6dfdc8d7037e096e7'
            'SKIP'
            'be0f6b8c0f0732e93605809d5a95ab2078842d27ec180ec39ff45d1e752cdab874db42c18660a8e73958be7cbd1a36b722f369db78faf198d576a15b1d85c447'
            'SKIP'
            'e1a7c2d46408332710d30a8ea965dee4509c8873100f224b7144111942f52cbfea8cdc4a514648ed751b3f0c562c21eb90916845ad5795d540d4a9b9f160cb75'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla’s GnuPG release key

_languages=(en-US zh-CN ja zh-TW)

for _lang in "${_languages[@]}"; do
  _locale=${_lang}
  _pkgname=thunderbird-nightly-${_locale,,}
  _src="${_name}-${_version}.${_lang}.linux"
  _filename="${_filename_prefix}${_src}-x86_64.tar.bz2"

  pkgname+=($_pkgname)
  source+=("${_filename}"::"${_url}/${_src}-x86_64.tar.bz2"
           "${_filename}.asc"::"${_url}/${_src}-x86_64.tar.bz2.asc")
  eval "package_$_pkgname() {
    msg2 'Removing old thunderbird directory...'
    rm -rf thunderbird
    msg2 'Extract ${_filename}...'
    bsdtar -xf ${_filename}
    _package $_lang
  }"
done

# Don't extract anything because they'll override each other
noextract=(${source[@]%%::*})

_package() {
  OPT_PATH="opt/${pkgbase}"
  pkgdesc="Standalone Web Browser from Mozilla — Nightly build ($1)"
  url="https://www.mozilla.org/$1/${_name}/${_channel}"
  provides=(thunderbird-${_channel}=$pkgver)
  conflicts=(thunderbird-${_channel} thunderbird-${_channel}-i18n-${1,,})
  replaces=(thunderbird-${_channel}-i18n-${1,,})

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${pkgbase}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${pkgbase}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128; do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${pkgbase}.png
  done

  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/defaults/preferences/vendor.js
pref("intl.locale.requested", "");
EOF

  # Disable auto-updates
  cat <<EOF | install -Dm644 /dev/stdin "${pkgdir}"/${OPT_PATH}/distribution/policies.json
{"policies": {"DisableAppUpdate": true}}
EOF

  # Use system-provided dictionaries
  rm -rf "${pkgdir}"/${OPT_PATH}/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "${pkgdir}"/${OPT_PATH}/dictionaries
  ln -sf /usr/share/hyphen "${pkgdir}"/${OPT_PATH}/hyphenation
}
