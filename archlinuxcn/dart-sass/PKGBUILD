# Maintainer: Thayne McCombs <astrothayne@gmail.com>
# Contributor: Ariel AxionL <i at axionl dot me>

pkgname=dart-sass
pkgver=1.26.11
pkgrel=1
pkgdesc="Sass makes CSS fun again (canonical implementation)"
arch=('x86_64')
url="https://github.com/sass/dart-sass"
license=('MIT')
makedepends=('dart>=2.6.0')
provides=('sass')
conflicts=('ruby-sass')
options=(!strip)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sass/${pkgname}/archive/${pkgver}.tar.gz")

sha256sums=('8c423ab1db190870000ce3b04d1779af4433a1a1a3be36be6c3211839a40005c')

build() {
    cd "${pkgname}-${pkgver}"
    pub get
    dart2native "bin/sass.dart" -Dversion=${pkgver} -o "bin/sass"
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 "bin/sass" "${pkgdir}/usr/bin/sass"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
