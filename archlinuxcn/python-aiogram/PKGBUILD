_name=aiogram
pkgname=python-aiogram
pkgver=2.11.1
pkgrel=1
pkgdesc="Is a pretty simple and fully asynchronous framework for Telegram Bot API"
arch=(any)
url="https://github.com/aiogram/aiogram"
license=('MIT')
depends=('python-aiohttp' 'python-babel')
makedepends=('python-setuptools')
optdepends=('python-aiohttp-socks: support for socks proxy')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/aiogram-2.11.1.tar.gz")
sha256sums=('4f717274040ce263e06ecc05a90f15f9836426d60f08a4800b133d419f49ed4f')

prepare() {
  cd "$srcdir/aiogram-2.11.1"
  sed -i '/certifi/d' setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i aiogram/bot/base.py
}

build() {
  cd "$srcdir/aiogram-2.11.1"
  python3 setup.py build
}

package() {
  cd "$srcdir/aiogram-2.11.1"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

