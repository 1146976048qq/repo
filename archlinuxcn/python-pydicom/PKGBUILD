# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pydicom
_pkgname=pydicom
pkgver=1.3.0
pkgrel=2
pkgdesc="Read, modify and write DICOM files with python code"
arch=(any)
url="https://github.com/pydicom/pydicom"
license=(MIT)
optdepends=(
    "python-numpy: for working with pixel data"
    "python-pillow: for working with compressed image data"
)
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-pytest-cov")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydicom/pydicom/archive/v${pkgver}.tar.gz")
sha512sums=('bff4925ed77aae812a507c1501db0772964f1d76637f7eaf1b978bbcf080e2fff6f15f4a35c14b4828913114817c76a6d79df8e94c3c2f6a173949d8a084dc63')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}"
  make test-code
  make test-coverage
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
