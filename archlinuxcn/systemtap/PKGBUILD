# Maintainer  : George Angelopoulos <george@usermod.net>
# Contributor : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor : dront78 <dront78@gmail.com>
pkgname=systemtap
pkgver=4.2
pkgrel=3
pkgdesc="provides infrastructure to simplify the gathering of information about the running system."
url="http://sourceware.org/systemtap/"
arch=('x86_64')
license=('GPL')
depends=('elfutils' 'nss' 'python2' 'json-c' 'avahi' 'readline' 'nspr')
makedepends=('python2-setuptools' 'xmlto' 'cpio')
optdepends=('sqlite3: for storing results in a database')
source=("${pkgname}-${pkgver}.tar.gz::https://sourceware.org/systemtap/ftp/releases/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://sourceware.org/systemtap/ftp/releases/${pkgname}-${pkgver}.tar.gz.asc"
        "systemtap.sysusers")
sha512sums=('50d426e8c3d976fd635f6867fb4202832be455d869178a384159d1b2cd2a25642b07da2bd3184081b13bfb95df48a97f430b06713eaee0d0f67e27f43266cfd6'
            'ddb3217d9bbe7b9531b3d7ce24d88766e65629bd720228c0f8f96edc82216e13e5f0d0f30c8012cb85bf5ac7401f5a551aa69fc0505a29475ed61c548b4cdcf4'
            '0b54000f0bb6ed9eb4f43dbdc2fd66fcc178fc24688610fcb7b2ace5195bf96a956e24f2c281545db1a5a7c98aa951af63e8bd00de1f0347bae6f379f3671344')
install='systemtap.install'
validpgpkeys=('F75E6545B9F8AA15AA932A444DE16D68FDBFFFB8'
              '5D38116FA4D3A7CC77E378D37E83610126DCC2E8')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/"${pkgname}" \
    --libdir=/usr/lib/"${pkgname}" \
    --mandir=/usr/share/man/ \
    --localstatedir=/var \
    --enable-pie \
    --disable-docs \
    --enable-htmldocs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/var"
  rm -rf "${pkgdir}/etc"

  # glibc 2.30-2 already provides these two files
  rm -f "$pkgdir"/usr/include/sys/sdt-config.h
  rm -f "$pkgdir"/usr/include/sys/sdt.h

  # group id 156 is stapusr
  chgrp 156 "${pkgdir}"/usr/bin/stapbpf
  chmod 04110 "${pkgdir}"/usr/bin/stapbpf

  chgrp 156 "${pkgdir}"/usr/bin/staprun
  chmod 04110 "${pkgdir}"/usr/bin/staprun

  cd "${srcdir}"
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}

