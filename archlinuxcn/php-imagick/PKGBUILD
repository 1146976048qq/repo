# Maintainer: Bruce Zhang <zttt183525594@gmail.com>

pkgname=php-imagick
_extname=imagick
pkgver=3.4.3
pkgrel=3
pkgdesc="PHP extension for IMagick"
arch=('x86_64')
url="https://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php' 'imagemagick>=6.5.3')
backup=("etc/php/conf.d/${_extname}.ini")
install="php-${_extname}.install"
source=("https://github.com/mkoppanen/${_extname}/archive/${pkgver}.tar.gz")
md5sums=('72871a70f5fbec8277b52c7f26492183')

build() {
  cd "${_extname}-${pkgver}"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "#extension=${_extname}.so" > "${_extname}.ini"
  install -Dm644 "${_extname}.ini" "${pkgdir}/etc/php/conf.d/${_extname}.ini"
}
