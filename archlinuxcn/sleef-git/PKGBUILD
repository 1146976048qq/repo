# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgname=sleef-git
_realver=3.4.1
pkgver=3.4.1.22.gcc5f3cd
pkgrel=2
pkgdesc='SIMD Library for Evaluating Elementary Functions, vectorized libm and DFT'
arch=('i686' 'x86_64')
url="https://sleef.org"
license=('custom')
depends=()
makedepends=(cmake git)
provides=(
  "sleef=${_realver}")
conflicts=("sleef")
options=('debug' 'strip')
source=("git://github.com/shibatch/sleef"
        cflags.patch)
md5sums=('SKIP'
         'c91ad1089e9213dd7da438bfc5de9e32')

pkgver() {
  cd sleef

  git describe --tags | sed -e 's/-/./g'
}

prepare() {
  cd sleef

  patch -Np1 < ../cflags.patch
}

build() {
  cd sleef

  mkdir -p build
  cd build

  CFLAGS+=" -ffile-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  CFLAGS+=" -fmacro-prefix-map=${srcdir}=${DBGSRCDIR:-/usr/src/debug}"
  export CFLAGS

  cmake .. \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=Off \
    -DBUILD_QUAD=On
  make
}

package() {
  cd sleef/build

  make DESTDIR="$pkgdir/" install
}
