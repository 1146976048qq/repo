# Maintainer: Ingo BÃ¼rk <admin at airblader dot de>

pkgname=i3-gaps-next-git
pkgver=4.16.1.r122.g573fbcc3
pkgrel=1
pkgdesc='A fork of i3wm tiling window manager (development branch) with more features, including gaps'
arch=('i686' 'x86_64')
url='https://github.com/Airblader/i3/tree/gaps-next'
license=('BSD')
provides=('i3-wm')
conflicts=('i3-wm' 'i3bar' 'i3bar-git' 'i3-git' 'i3-gaps-git' 'i3-gaps')
groups=('i3-vcs')
depends=('xcb-util-keysyms' 'xcb-util-wm' 'libev' 'yajl'
         'startup-notification' 'pango' 'perl' 'xcb-util-cursor' 'xcb-util-xrm'
         'libxkbcommon-x11')
makedepends=('git' 'asciidoc' 'docbook-xsl' 'xmlto' 'perl' 'pkgconfig')
optdepends=('rxvt-unicode: The terminal emulator used in the default config.'
            'dmenu: As menu.'
            'i3lock: For locking your screen.'
            'i3status: To display system information with a bar.'
            'perl-json-xs: For i3-save-tree'
            'perl-anyevent-i3: For i3-save-tree')
options=('docs' '!strip')
source=('git://github.com/Airblader/i3#branch=gaps-next')
sha1sums=('SKIP')

_gitname='i3'

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_gitname"
  # apply patch from:  https://github.com/i3/i3/issues/3652
  # see also https://aur.archlinux.org/packages/i3-gaps-next-git
  patch -F5 -Np1 <<'EOF'
diff --git a/Makefile.am b/Makefile.am
index ee71b7f2..79201b98 100644
--- a/Makefile.am
+++ b/Makefile.am
@@ -277,6 +277,8 @@ i3_LDADD = \

 libi3_CFLAGS = \
    $(AM_CFLAGS) \
+   $(GLIB2_CFLAGS) \
+   $(GOBJECT2_CFLAGS) \
    $(XCB_CFLAGS) \
    $(XCB_UTIL_CFLAGS) \
    $(XCB_UTIL_XRM_CFLAGS) \
@@ -285,6 +287,8 @@ libi3_CFLAGS = \

 libi3_LIBS = \
    $(top_builddir)/libi3.a \
+   $(GLIB2_LIBS) \
+   $(GOBJECT2_LIBS) \
    $(XCB_LIBS) \
    $(XCB_UTIL_LIBS) \
    $(XCB_UTIL_XRM_LIBS) \
diff --git a/configure.ac b/configure.ac
index 6088699d..556b4be3 100644
--- a/configure.ac
+++ b/configure.ac
@@ -101,6 +101,8 @@ PKG_CHECK_MODULES([XKBCOMMON], [xkbcommon xkbcommon-x11])
 PKG_CHECK_MODULES([YAJL], [yajl])
 PKG_CHECK_MODULES([LIBPCRE], [libpcre >= 8.10])
 PKG_CHECK_MODULES([PANGOCAIRO], [cairo >= 1.14.4 pangocairo])
+PKG_CHECK_MODULES([GLIB2], [glib-2.0])
+PKG_CHECK_MODULES([GOBJECT2], [gobject-2.0])

 # Checks for programs.
 AC_PROG_AWK
EOF

}

build() {
  cd "$_gitname"

  autoreconf --force --install

  rm -rf build/
  mkdir -p build && cd build/

  ../configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-sanitizers

  # See https://lists.archlinux.org/pipermail/arch-dev-public/2013-April/024776.html
  make CPPFLAGS+="-U_FORTIFY_SOURCE"
}

package() {
  cd "$_gitname"
  cd build/

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

