# Maintainer: Lorenzo Fontana <lo@linux.com>
# Maintainer: Xuanwo <xuanwo@archlinuxcn.org>
pkgname=coredns
pkgver=1.5.0
pkgrel=1
pkgdesc='A DNS server that chains plugins'
makedepends=('go' 'make' 'git')
conflicts=('coredns-bin')
arch=('i686' 'x86_64')
url='https://github.com/coredns/coredns'
license=('Apache')
provides=('coredns')
backup=(etc/coredns/Corefile)
source=(coredns-${pkgver}.tar.gz::https://github.com/coredns/${pkgname}/archive/v${pkgver}.tar.gz
        coredns.service
        coredns-sysusers.conf
        Corefile)
sha256sums=('69d9a7df50ecb8cc44656064537927e2abd5ff5d6b82de067e6328723b81efe3'
            '947b4838af48212ecd8e565155c97ca61ff7d408d0b8622499925b6e232656c4'
            '178c632fd855a5a35ae69e13ab554d5abad696cf75d23c6b6b37f0ace3b4b0f2'
            'd53c611f35fe67b5f631dd2e4731f75de72c3f7d07ee10f21b11360c690bfc45')

build() {
  export GOPATH="$srcdir/build"
  export PATH=$GOPATH/bin:$PATH

  cd $pkgname-${pkgver}
  # In order to build with PIE buildmode, we need to use CGO.
  # Currently, we will keep cgo disabled for better performance.
  GO111MODULE=on CGO_ENABLED=0 go build -v -ldflags="-s -w" -o coredns
}

package() {
  # Install binary
  install -Dm755 "$srcdir/$pkgname-${pkgver}/coredns" "$pkgdir/usr/bin/coredns"
  # Install systemd service
  install -Dm644 "$srcdir/coredns.service" "$pkgdir/usr/lib/systemd/system/coredns.service"
  # Install coredns sysusers
  install -Dm644 "$srcdir/coredns-sysusers.conf" "$pkgdir/usr/lib/sysusers.d/coredns.conf"
  # Install default coredns config
  install -Dm644 Corefile "$pkgdir/etc/coredns/Corefile"
}

# vim: ft=sh syn=sh et
