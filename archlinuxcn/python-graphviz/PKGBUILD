# Maintainer: Hu Butui <hot123tea123@gmail.com>

pkgname=python-graphviz
_pkgname=graphviz
pkgver=0.13.2
pkgrel=2
pkgdesc='Simple Python interface for Graphviz.'
arch=(any)
url='https://github.com/xflr6/graphviz'
license=(MIT)
depends=('graphviz')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-cov' 'python-pytest-mock')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/xflr6/${_pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('bb932d6833e7c62ac6b53624b6d55bfd81f40e861f55642e1221ac4f4d8421b4')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
