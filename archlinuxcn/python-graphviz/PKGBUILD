# Maintainer: Hu Butui <hot123tea123@gmail.com>

pkgname=python-graphviz
_pkgname=graphviz
pkgver=0.13
pkgrel=1
pkgdesc='Simple Python interface for Graphviz.'
arch=(any)
url='https://github.com/xflr6/graphviz'
license=(MIT)
depends=('graphviz')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-cov' 'python-pytest-mock')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/xflr6/${_pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('e1afdadc1dcd4a4193f967f421bf6688d259685d8d8d989b8a57f7d2cf64d1ca')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
