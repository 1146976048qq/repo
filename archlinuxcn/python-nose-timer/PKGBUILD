# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: Andrew Sun <adsun701@gmail.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=python-nose-timer
_pkgname=nose-timer
pkgver=0.7.5
pkgrel=3
pkgdesc='Timer plugin for nosetests'
arch=(any)
url='https://github.com/mahmoudimus/nose-timer'
license=(MIT)
depends=(python-nose)
makedepends=(python-setuptools)
checkdepends=(python-mock python-parameterized python-pytest)
source=("https://github.com/mahmoudimus/nose-timer/archive/v${pkgver}.tar.gz")
sha512sums=('25fa639d15a01492bde0d47b262c579061eaf21be6953c2451a56aa3be43a09c1e730e4a053f0005f6d8482100f4b8546465456755169df6a66b798313569c49')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim: ts=2 sw=2 et:
