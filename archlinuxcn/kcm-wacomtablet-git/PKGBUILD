# Maintainer: Megumifox <i@megumifox.com>

pkgname=kcm-wacomtablet-git
pkgver=716.20d6c28
pkgrel=1
epoch=1
pkgdesc="KDE GUI for the Wacom Linux Drivers"
arch=('i686' 'x86_64')
url="https://www.linux-apps.com/p/1127862/"
license=('GPL')
depends=('plasma-framework' 'xf86-input-wacom' 'libx11')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'kdoctools')
provides=('kcm-wacomtablet')
conflicts=('kcm-wacomtablet')
replaces=('kcm-wacomtablet-frameworks-git')
source=('git://anongit.kde.org/wacomtablet.git'
         'wacomtablet-3.1.1-xlib-optional.patch'
         'add_missing_includes.diff')
md5sums=('SKIP'
         'a196a7931aeda4c922552ca28f353862'
         '2ce92eaa8446aa530a5849e7bbc0e937')

pkgver() {
  cd "$srcdir/wacomtablet"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/wacomtablet"
  patch -Np1 < "$srcdir/wacomtablet-3.1.1-xlib-optional.patch"
  patch -Np1 < "$srcdir/add_missing_includes.diff"
}

build() {
  cd "$srcdir/wacomtablet"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/wacomtablet"

  make DESTDIR="$pkgdir" install
}

