# Maintainer: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>
pkgname=bpftrace
pkgver=0.11.1
pkgrel=1
pkgdesc='High-level tracing language for Linux eBPF'
arch=('i686' 'x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache')
depends=('libelf' 'zlib' 'llvm-libs' 'clang' 'bcc')
makedepends=('cmake' 'llvm' 'git')
optdepends=('libbpf>=0.0.5: BTF support (required at compile time)')
conflicts=('bpftrace-git')
provides=('bpftrace')
source=("https://github.com/iovisor/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('361cedc6ce8a950188717db5ffd005b9709371010810cf6bc870ca945c1db9bdc7ab99b55b694345d580d03145b3eb6ca169f2f1f9909f3e9fc34250df9999f3')
options=('!strip')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir -p build
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}/" install
  find ${pkgdir}/usr/share/man/man8 -name '*.8.gz' | awk -F '.' '{printf("mv %s.8.gz %s-bpftrace.8.gz\n",$1,$1)}' | sh
}

# vim:set ts=2 sw=2 et:
