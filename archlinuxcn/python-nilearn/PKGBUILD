# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-nilearn
_pkgname=nilearn
pkgver=0.6.0
pkgrel=2
pkgdesc='Machine learning for NeuroImaging in Python'
arch=('any')
url='https://github.com/nilearn/nilearn'
license=('BSD')
depends=(
  'python-joblib'
  'python-matplotlib'
  'python-nibabel'
  'python-numpy'
  'python-pandas'
  'python-pillow'
  'python-pydicom'
  'python-scikit-learn'
  'python-scipy'
)
checkdepends=(
  'python-coverage'
  'python-nose'
  'python-pytest'
)
makedepends=(
  'cython'
  'python-setuptools'
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/nilearn/nilearn/archive/${pkgver}.tar.gz")
sha512sums=('41c497ee0934d1df97e40fa56cfa1c67a22cd3a6e3921999a1e3833bf6f1bd32f0e2e1edc2e93f5116963bc843099cc6c6a242baf5881c1ff8ccdba76e6f4bce')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
