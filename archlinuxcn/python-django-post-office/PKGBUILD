_name=django-post-office
pkgname=python-django-post-office
pkgver=3.5.2
pkgrel=3
pkgdesc="A Django app to monitor and send mail asynchronously, complete with template support."
arch=(any)
url="https://github.com/ui/django-post_office"
license=('MIT')
depends=('python-django-jsonfield')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/django-post_office-3.5.2.tar.gz")
sha256sums=('9e5b84d6153a457674266d3a31f1543a9ff35cc17d5c2fa4a5e5023683d6b10e')

build() {
  cd "$srcdir/django-post_office-3.5.2"
  python3 setup.py build
}

package() {
  cd "$srcdir/django-post_office-3.5.2"
  python3 setup.py install --root=$pkgdir --optimize=1 --skip-build

  # make sure we don't install any world-writable or root-readable-only files
  # we shouldn't need to fix ownership as we extract tarballs as a non-root user
  # https://github.com/pypa/setuptools/issues/1328
  # https://github.com/LonamiWebs/Telethon/issues/1605
  chmod u=rwX,go=rX -R "$pkgdir"
  # make sure we don't install annoying files
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$_site_packages/tests/"
}

