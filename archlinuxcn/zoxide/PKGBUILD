# Maintainer: Hao Long <imlonghao@archlinuxcn.org>

pkgname=zoxide
pkgver=0.5.0
pkgrel=1
pkgdesc="A fast cd command that learns your habits"
arch=("x86_64" "i686")
url="https://github.com/ajeetdsouza/zoxide"
license=("MIT")
depends=("gcc-libs")
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('62b7a2ced73d5ac0a183b3855d54d6619166b4d8d8c74299bb610265ccc4b193')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked --all-features --target-dir=target
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm755 target/release/zoxide -t "${pkgdir}/usr/bin"
}
