# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2-julia
pkgver=0.27.8
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=(i686 x86_64 armv7h aarch64)
url='https://libgit2.github.com/'
depends=(zlib openssl libssh2 curl http-parser)
makedepends=(cmake python)
provides=("libgit2=$epoch:$pkgver")
conflicts=(libgit2)
license=('GPL2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        libgit2-agent-nonfatal.patch)
sha256sums=('8313873d49dc01e8b880ec334d7430ae67496a89aaa8c6e7bbd3affb47a00c76'
            'a60e169812b802e3fcb4ee7501786346eb87db9ffa3c8ae3311de47ce5a15f10')
options=(!strip)

prepare() {
  cd "libgit2-$pkgver"

  patch -p1 < ../libgit2-agent-nonfatal.patch
}

build() {
  cd "libgit2-$pkgver"
  export LANG=en_US.UTF-8
  export CFLAGS+=' -g'
  export CXXFLAGS+=' -g'
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

package() {
  cd "libgit2-$pkgver"
  make DESTDIR="$pkgdir" install
}
