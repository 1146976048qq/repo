# Maintainer: metal A-wing <1 at 233 dot email>
# Contributor: Joaquin Garmendia <joaquingc123 at gmail dot com>
# Contributor: Ariel AxionL <i [at] axionl [dot] me>

pkgname=annie
pkgver=0.10.0
pkgrel=1
arch=('x86_64')
pkgdesc="A fast, simple and clean video downloader written in Go"
url="https://github.com/iawia002/annie"
license=("MIT")
makedepends=("go-pie")
depends=("ffmpeg")
provides=("annie")
conflicts=("annie")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('5be6dbee25397ec9942bdb8a7352952306ec33586b0fe4d54d54b6cb39a28b08')

build(){
  cd "${pkgname}-${pkgver}"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" .
}

package(){
  cd "${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
