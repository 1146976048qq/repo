# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Florian Pritz
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad-oce
epoch=1
pkgver=0.18.5
pkgrel=2
pkgdesc='General purpose 3D CAD modeler'
arch=(x86_64)
url='https://freecadweb.org/'
license=(LGPL)
depends=(boost-libs glew jsoncpp libspnav med netcdf curl oce openmpi
         pyside2-tools python-matplotlib python-pivy python-pyside2 qt5-svg
         qt5-tools qt5-webkit qt5-x11extras shared-mime-info xerces-c
         hicolor-icon-theme vtk)
makedepends=(boost cmake coin eigen gcc-fortran git ninja pyside2 desktop-file-utils
             python-shiboken2 shiboken2 swig)
optdepends=(python-matplotlib graphviz openscad)
options=(!libtool !staticlibs debug strip)
provides=("freecad=$pkgver")
conflicts=(freecad)
source=("git+https://github.com/FreeCAD/FreeCAD.git#tag=$pkgver"
        'FindPySide2Tools.cmake'
        "$pkgname-py39.patch"
        "$pkgname-boost-1.73.patch"
        "freecad.desktop" "freecad.xml")
sha256sums=('SKIP'
            '421a364ad214a25be578d81edc9960daab87d7b26f341022aa22dd15b9df621b'
            'de1d61c18d329e0903d5c1bc27b9c4e28c4911380bdd4b6c49da5b63740862f1'
            '99a55f06e9d3be3385cb41d747b1a6c9515b010d867bdeeda000a45c0014a89e'
            '7ef6f240a82a861cbeca128e453da1a0c37f548876894f409a780c55e9909add'
            '4ee1daf47c8371a3e17173d85a0dd4a106dacd7899d783ccd97a6f3e0dd1a21f')

prepare() {
  cd FreeCAD

  patch -Np1 -i ../$pkgname-py39.patch
  patch -Np1 -i ../$pkgname-boost-1.73.patch

  # Use the fixed version of FindPySide2Tools.cmake
  cp -f "$srcdir/FindPySide2Tools.cmake" cMake/FindPySide2Tools.cmake

  # Fix Qt5 version detection
  sed -i 's/Qt5Core_VERSION LESS/Qt5Core_VERSION VERSION_LESS/g' \
      cMake/FindPySide2Tools.cmake

  cd src

  # Update swigpyrun.in for Python 3.8
  sed -i 's/interp->modules/PyImport_GetModuleDict()/g' Base/swigpyrun.inl

  # Fix for Qt 5.15
  sed -i 's/\(.*\)include <QPainter>$/&\n\1include <QPainterPath>/' \
      Mod/Image/Gui/OpenGLImageBox.cpp

  # Compensate for the unusual Shiboken versioning scheme
  sed -i 's/SHIBOKEN_MICRO_VERSION/0/g' Gui/WidgetFactory.cpp

  # Fix missing "std::" qualifiers, sledge hammer style
  find . \
       ! -wholename "*/Mod/Sketcher/*" \
       ! -wholename "*/Mod/PartDesign/*" \
       -name "*.cpp" -type f \
       -exec sed -i '1i using namespace std;' {} \;

  # Add missing include
  sed -i '1i \#include <Inventor\/SbByteBuffer.h>' \
      Gui/Quarter/QuarterWidget.cpp
}

build() {
  mkdir -p build
  cd build

  cmake ../FreeCAD \
        -Wno-dev \
        -DBUILD_QT5=ON \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
        -DCMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
        -DCMAKE_INSTALL_DATADIR="/usr/share/freecad" \
        -DCMAKE_INSTALL_DOCDIR="/usr/share/freecad/doc" \
        -DCMAKE_INSTALL_PREFIX="/usr/lib/freecad" \
        -DCMAKE_INSTALL_LIBDIR="/usr/lib/freecad/lib" \
        -DCMAKE_INSTALL_INCLUDEDIR="/usr/include" \
        -DFREECAD_USE_EXTERNAL_PIVY=ON \
        -DFREECAD_USE_OCC_VARIANT="Official Version" \
        -DFREECAD_USE_QT_FILEDIALOG=ON \
        -DPYTHON_EXECUTABLE=/usr/bin/python \
        -DOCC_INCLUDE_DIR=/usr/include/oce/ \
        -DOCE_DIR=/usr/lib/oce-0.18-dev/ \
        -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Package desktop shortcut
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/freecad.desktop"

  # Package MIME info
  install -Dm644 freecad.xml "$pkgdir/usr/share/mime/packages/freecad.xml"

  cd FreeCAD/src/Gui/Icons

  # Package icons
  for i in 16 32 48 64; do
    install -Dm644 "freecad-icon-$i.png" \
            "$pkgdir/usr/share/icons/hicolor/${i}x$i/apps/freecad.png"
  done
  install -Dm644 freecad.svg \
          "$pkgdir/usr/share/icons/hicolor/scalable/apps/freecad.svg"


  # Package symlinks in /usr/bin
  install -dm755 "${pkgdir}/usr/bin"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/freecad"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/FreeCAD"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/freecadcmd"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/FreeCADCmd"

  boostver=$(objdump -p /usr/lib/libboost_system.so | \
               grep SONAME | sed -e 's/.*boost_system\.so\.//')
  [[ $boostver =~ ^([0-9]*)\.([0-9]*)\.([0-9]*) ]]
  maj_ver=${BASH_REMATCH[1]}
  min_ver=${BASH_REMATCH[2]}
  patch_ver=${BASH_REMATCH[3]}
  ((next_min=min_ver + 1))
  depends+=("boost-libs>=${maj_ver}.${min_ver}.${patch_ver}"
            "boost-libs<${maj_ver}.${next_min}.0")

  pymin_ver=$(python -c 'import sys; print(sys.version_info.minor)')
  ((pynext_min=pymin_ver + 1))
  depends+=("python>=3.${pymin_ver}.0" "python<3.${pynext_min}.0")
}
