#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=python3-xcpf
pkgver=2018.12
pkgrel=2
pkgdesc='Xyne'"'"'s common Pacman functions, for internal use.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/python3-xcpf"
depends=(pyalpm python-xdg python3 python3-memoizedb python3-xcgf)
optdepends=('rsync: Retrieve ABS files via rsync.')
source=(
  https://xyne.archlinux.ca/projects/python3-xcpf/src/python3-xcpf-2018.12.tar.xz
  https://xyne.archlinux.ca/projects/python3-xcpf/src/python3-xcpf-2018.12.tar.xz.sig
  "pyalpm.patch"
)
md5sums=('96fa5965a647784377c4c89219e98606'
         'SKIP'
         'a78f286bb0dc5175f07856d2f2ef30fd')
sha512sums=('33a93c6b3adb5ebaf678159ad8d58314f26f7db2764b147024c61912f839148e00384c2ec79a7c1b884fc951e36c93537d6350affa6af170af0f23c11ed6fe30'
            'SKIP'
            '7a2a14f95696804c9c28fbf574472859b55d4757e1fb1216c6ea513cdf881d94738a3e9f02d902ccd612854ae47ea384d6e36ba242f43d2e56f8d25355b50bed')
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../pyalpm.patch
}

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

# vim: set ts=2 sw=2 et:
