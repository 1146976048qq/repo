# Maintainer: Jan Neumann <neum dot ja at gmail dot com>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock-git
_gitname=latte-dock
pkgver=0.9.11.r4096.51a302de
pkgrel=1
pkgdesc='Latte is a dock based on plasma frameworks that provides an elegant and intuitive experience for your tasks and plasmoids'
arch=('i686' 'x86_64')
url='https://cgit.kde.org/latte-dock.git/'
license=('GPL')
depends=('plasma-framework>=5.38.0' 'plasma-desktop')
optdepends=('libunity: quicklists, counters, and progress bars for apps using libunity')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'python' 'subversion')
conflicts=('latte-dock')
provides=('latte-dock')
source=("git://anongit.kde.org/latte-dock.git")

sha256sums=('SKIP')

pkgver() {
    cd ${_gitname}
    printf "%s.r%s.%s" $(git describe --tags $(git rev-list --tags --max-count=1) | sed 's/^v//g') $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
    cd ${_gitname}
    mkdir -p build && cd build

    # Upstream requirements do not currently support ruby 2.7.0, disable translations.
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release ..

    # cmake -DCMAKE_INSTALL_PREFIX=/usr \
    #     -DKDE_L10N_BRANCH=trunk \
    #     -DKDE_L10N_AUTO_TRANSLATIONS=OFF \
    #     -DCMAKE_BUILD_TYPE=Release ..
    # make fetch-translations
}

package() {
    make -C ${_gitname}/build DESTDIR=${pkgdir} install
}
