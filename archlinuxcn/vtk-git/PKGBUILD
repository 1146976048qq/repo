# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=(vtk-git vtk-cuda-git)
_pkgver=9.0.0
pkgver=9.0.0.r75220.dcd7ed501c
pkgrel=1
pkgdesc='A software system for 3D computer graphics, image processing, and visualization'
arch=('x86_64')
url='http://www.vtk.org'
license=('BSD')
depends=(
  'adios2'
  'double-conversion'
  'ffmpeg'
  'freetype2'
  'glew'
  'jsoncpp'
  'libjpeg'
  'libtheora'
  'libtiff'
  'netcdf'
  'openslide'
  'openturns'
  'ospray'
  'pdal'
  'pegtl'
  'postgresql'
  'proj'
  'pugixml'
  'qt5-base'
  'python'
  'sqlite'
  'unixodbc'
)
makedepends=(
  'boost'
  'cmake'
  'cuda'
  'eigen'
  'gcc-fortran'
  'git'
  'hdf5'
  'libogg'
  'libxml2'
  'libxt'
  'ocl-icd'
  'openmpi'
  'pugixml'
  'python-mpi4py'
  'qt5-webkit'
  'qt5-x11extras'
  'utf8cpp'
  'wget'
)
optdepends=(
  'hdf5'
  'openmpi'
  'qt5-webkit'
  'qt5-x11extras'
)
provides=(vtk=${pkgver})
conflicts=(vtk)
source=("git+https://gitlab.kitware.com/vtk/vtk.git")
sha256sums=('SKIP')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
  cd "${srcdir}/vtk"
  git submodule update --init --recursive
  # remove old GPU archs
  sed -i -e '242,248s/--generate-code=arch=compute_30,code=sm_30//' \
    -e '242,248s/--generate-code=arch=compute_35,code=sm_35//' \
    -e '242,248s/--generate-code=arch=compute_50,code=sm_50//' \
    -e '242,248{/^[[:space:]]*$/d}' \
    "${srcdir}/vtk/ThirdParty/vtkm/vtkvtkm/vtk-m/CMake/VTKmDeviceAdapters.cmake"
}

pkgver() {
  cd "${srcdir}/vtk"
  printf "%s.r%s.%s" ${_pkgver} "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake_opts=(
    -DBUILD_SHARED_LIBS=ON
    -DBUILD_TESTING=OFF
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_SKIP_INSTALL_RPATH=ON
    -DVTK_BUILD_ALL_MODULES=ON
    -DVTK_BUILD_TESTING=OFF
    -DVTK_LEGACY_REMOVE=ON
    -DVTK_MODULE_ENABLE_VTK_FiltersOpenTURNS=YES
    -DVTK_MODULE_ENABLE_VTK_IOLAS=NO
    -DVTK_MODULE_ENABLE_VTK_PoissonReconstruction=NO
    -DVTK_MODULE_ENABLE_VTK_RenderingOpenVR=NO
    -DVTK_MODULE_USE_EXTERNAL_VTK_gl2ps=OFF
    -DVTK_MODULE_USE_EXTERNAL_VTK_libharu=OFF
    -DVTK_PYTHON_VERSION="3"
    -DVTK_USE_EXTERNAL=ON
    -DVTK_USE_FFMPEG_ENCODER=ON
    -DVTK_USE_LARGE_DATA=ON
    -DVTK_USE_MPI=ON
    -DVTK_WRAP_PYTHON=ON
)
  
  cmake -B "${srcdir}/build-vtk-git" -S "${srcdir}/vtk" \
    -DVTK_USE_CUDA=OFF \
    ${cmake_opts[@]}
  make -C "${srcdir}/build-vtk-git"
  
  cmake -B "${srcdir}/build-vtk-cuda-git" -S "${srcdir}/vtk" \
    -DVTK_USE_CUDA=ON \
    -DCMAKE_C_COMPILER=/opt/cuda/bin/gcc \
    -DCMAKE_CXX_COMPILER=/opt/cuda/bin/g++ \
    -DCMAKE_CUDA_HOST_COMPILER=/opt/cuda/bin/g++ \
    -DVTKm_CUDA_Architecture=all \
    ${cmake_opts[@]}
  make -C "${srcdir}/build-vtk-cuda-git"
}

_package() {
  make -C "${srcdir}/build-${pkgname}" DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/vtk/Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  python -O -m compileall "${pkgdir}/usr/lib/python$(get_pyver)/site-packages"
}

package_vtk-git() {
  _package
}

package_vtk-cuda-git() {
  pkgdesc="${pkgdesc} (with CUDA)"
  depends+=(cuda)

  _package
}
# vim:set ts=2 sw=2 et:

