# Maintainer: Joffrey <j-off@live.fr>

pkgbase='python-django-statici18n'
pkgname=('python-django-statici18n' 'python2-django-statici18n')
pkgver='1.9.0'
pkgrel=1
pkgdesc='A Django app that provides helper for generating JavaScript catalog to static files'
arch=('any')
url='https://github.com/zyegfryed/django-statici18n'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/zyegfryed/django-statici18n/archive/v$pkgver.tar.gz")
sha256sums=('5dfad9c3c6c75fcf4f90941aeb4745517da8ec16e8da7eeecb42d05dbe26155c')

package_python-django-statici18n() {
    depends=(
        'python-django-appconf'
    )
    cd "$srcdir/django-statici18n-$pkgver"
    install -Dm644 './LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    python './setup.py' install --root="$pkgdir" --optimize=1
}

package_python2-django-statici18n() {
    depends=(
        'python2-django-appconf'
    )
    cd "$srcdir/django-statici18n-$pkgver"
    install -Dm644 './LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    python2 './setup.py' install --root="$pkgdir" --optimize=1
}
