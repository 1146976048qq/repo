# Maintainer: Bruce Zhang <zttt183525594@gmail.com>

pkgname=pulseaudio-modules-bt
pkgver=1.0
pkgrel=3
pkgdesc="PulseAudio Bluetooth modules with SBC, AAC, APTX, APTX-HD, Sony LDAC (A2DP codec) support"
arch=("i686" "x86_64" "arm" "armv6h" "armv7h" "aarch64")
url="https://github.com/EHfive/pulseaudio-modules-bt"
license=('LGPL')
depends=("pulseaudio>=12.0" "bluez>=5.0" "bluez-libs>=5.0" "sbc" "libfdk-aac")
makedepends=("cmake>=3.0" "make" "pkg-config" "libavcodec.so>=58-64" "libavutil.so>=56-64" "libtool" "libldac" "git")
optdepends=("libavcodec.so>=58-64" "libavutil.so>=56-64" "libldac")
conflicts=("pulseaudio-bluetooth")
provides=("pulseaudio-bluetooth")

source=("https://github.com/EHfive/pulseaudio-modules-bt/archive/v$pkgver.tar.gz"
        "git+https://github.com/pulseaudio/pulseaudio.git")

md5sums=('SKIP' 'SKIP')

prepare() {
    cd "$pkgname-v$pkgver"
    rm -r pa libldac
    ln -sf -T "../pulseaudio" "pa"
    ln -sf -T "../libldac" "libldac"
    cd "pa"
    git checkout v`pkg-config libpulse --modversion|sed 's/[^0-9.]*\([0-9.]*\).*/\1/'`
}

build() {
    cd "$pkgname-v$pkgver"
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DFORCE_NOT_BUILD_LDAC=ON \
        .
    make
}

package() {
    cd "$pkgname-v$pkgver"
    make DESTDIR="$pkgdir" install
}
