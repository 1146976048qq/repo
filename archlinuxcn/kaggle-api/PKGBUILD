# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: asermax <asermax@gmail.com>

pkgname=kaggle-api
_name=kaggle
pkgver=1.5.5
pkgrel=1
pkgdesc='Kaggle API CLI'
arch=('any')
url="https://github.com/Kaggle/kaggle-api"
license=('Apache')
depends=(
  'python'
  'python-urllib3'
  'python-six'
  'python-certifi'
  'python-dateutil'
  'python-requests'
  'python-tqdm'
  'python-slugify'
)
makedepends=(
  'python-setuptools'
)
source=(${pkgname}-${pkgver}.tar.gz::"https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d29d3d545f96cd9dab54ae335506c888f0721838929599bf3fd9220f96f037904f4f0544459e28c2b68c4de1ebe30843287ba66aaf48fd4752b4907d548a4944')

build() {	
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:

