# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: asermax <asermax@gmail.com>

pkgname=kaggle-api
_name=kaggle
pkgver=1.5.4
pkgrel=1
pkgdesc='Kaggle API CLI'
arch=('any')
url="https://github.com/Kaggle/kaggle-api"
license=('Apache')
depends=(
  'python'
  'python-urllib3'
  'python-six'
  'python-certifi'
  'python-dateutil'
  'python-requests'
  'python-tqdm'
  'python-slugify'
)
makedepends=(
  'python-setuptools'
)
source=(${pkgname}-${pkgver}.tar.gz::"https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('22980e3289c267186f986a19dacd6760f1a6eab2bc63f5a6f63499dadf87315928f5f397f8a05f3639f6149c5974159b143144a9a7fc845e0dbe376b06b43432')

build() {	
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:

