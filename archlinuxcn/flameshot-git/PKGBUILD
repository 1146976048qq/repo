# Maintainer: j1simon <stemp@ono.com>

_pkgname=flameshot
pkgname=flameshot-git
pkgver=r686.58b09bf
pkgrel=1
pkgdesc="Powerful yet simple to use screenshot software"
arch=('i686' 'x86_64')
url="https://github.com/lupoDharkael/flameshot"
license=('GPL')
depends=('qt5-base' 'qt5-svg')
makedepends=('qt5-tools' 'git' 'cmake')
provides=('flameshot-git')
conflicts=('flameshot')
source=(
  "git+https://github.com/flameshot-org/flameshot.git"
  "0001-correct-position-of-QScreen-geometry-on-HiDPI-multi-.patch"
)
sha256sums=('SKIP'
            'bee0669d3a16a1a7fabf97cec1f562b09471b9ebd2b283f8d7bbe6436d5f96fa')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  # Get the version number.
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  patch -p1 -N < "${srcdir}/0001-correct-position-of-QScreen-geometry-on-HiDPI-multi-.patch"
}

build() {
  cd "${srcdir}/${_pkgname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
