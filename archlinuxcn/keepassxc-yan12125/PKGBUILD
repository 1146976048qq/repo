# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

# This PKGBUILD is forked from aur/keepassxc-git. Original contributors are:
# Contributor: Daniel Landau <daniel@landau.fi>
# Contributor: Maxqia <contrib@maxqia.com>

# The following people have contributed to keepassx-git package
# Contributor: Lev Lybin <aur@devtrue.net>
# Contributor: Jamie Macdonald <jamie.alban@gmail.com>
# Contributor: Alucryd <alucryd at gmail dot com>
# Contributor: Paolo Stivanin <admin at polslinux dot it>

_pkgname=keepassxc
pkgname=$_pkgname-yan12125
pkgver=2.4.3.r32.g38524507
_commit=38524507d5e2c5d5b607a3747b37cc3a9126f70c
pkgrel=1
pkgdesc="A reboot of an open source password safe which helps you to manage your passwords in an easy and secure way"
arch=(x86_64)
url="https://keepassxc.org/"
license=(GPL2)
depends=(argon2 libgcrypt libsodium libxtst quazip qrencode qt5-svg qt5-x11extras
         yubikey-personalization zlib)
makedepends=(cmake qt5-tools git)
checkdepends=(xorg-server-xvfb openbox)
conflicts=("$_pkgname")
provides=("$_pkgname=$pkgver")
source=("git+https://github.com/keepassxreboot/keepassxc.git?signed#commit=$_commit"
        'multiple-urls-v1.patch'
        'run_test.sh')
sha256sums=('SKIP'
            '17a6c2fde82544fb17eb4ec527b8912e57555e8d1ea9cefcdf0ce8d463640b5d'
            '503272fd54f31a7c34c6c60bb5b9bc11169b34084de4e1417d766cc5a6f88029')
validpgpkeys=(
  'BF5A669F2272CF4324C1FDA8CFB4C2166397D0D2'  # KeePassXC Release <release@keepassxc.org>
  '3489040C1F0C3EEAB8D556A5440FC65F2E0C6E01'  # Jonathan White <support@dmapps.us>
  'B3724F5B2C98B5B0AEDDBD582AC0283D4CFC9A10'  # Janek Bevendorff <janek@jbev.net> (https://github.com/phoerious)
)

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-beta/beta/;s/-/./g'
  )
}

prepare() {
  cd $_pkgname
  mkdir -p build

  patch -Np1 -i ../multiple-urls-v1.patch
}

build() {
  cd $_pkgname/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_DEV_BUILD=ON \
    -DWITH_GUI_TESTS=ON \
    -DWITH_XC_ALL=ON \
    -DWITH_XC_KEESHARE_SECURE=ON \
    -DWITH_XC_UPDATECHECK=OFF \
    ..

  make
}

check() {
  cd $_pkgname/build

  xvfb-run -a bash ../../run_test.sh
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
