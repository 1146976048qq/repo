# Maintainer: Fabius
# Contributor: Adria Arrufat <swiftscythe @t gmail d@t com>
# Contributor: Gordin <9ordin @t gmail d@t com>

pkgname=screenkey
pkgver=1.1
pkgrel=2
pkgdesc="A screencast tool to display your keys inspired by Screenflick"
arch=('any')
url="https://gitlab.com/screenkey/screenkey"
license=('GPL3')
depends=('python' 'python-gobject' 'gtk3' 'python-cairo' 'libx11')
makedepends=('python-setuptools' 'python-distutils-extra')
optdepends=('slop' 'ttf-font-awesome' 'libappindicator-gtk3')
source=("https://www.thregr.org/~wavexx/software/$pkgname/releases/$pkgname-$pkgver.tar.gz"
        "https://www.thregr.org/~wavexx/software/$pkgname/releases/$pkgname-$pkgver.tar.gz.asc")
sha256sums=('0f382e485bf185d9f4f549eab9cfe1a48c53d66dc9c362e6ab433e51c6842ed7'
            'SKIP')
validpgpkeys=('41958F4116FDD1B43DEE754A2BB7D6F2153410EC')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --skip-build --optimize=1 --root="$pkgdir/"
}
