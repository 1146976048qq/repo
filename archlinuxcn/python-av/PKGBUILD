# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=8.0.1
pkgrel=1
arch=("x86_64")
license=("BSD")
makedepends=(
  'cython'
  'python-setuptools'
)
checkdepends=(
  'python-pytest'
)
depends=(
  'ffmpeg'
  'python-numpy'
  'python-pillow'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('05484ecdb87f3b02b040d81d16d653d09902f10885168ae9471f53d4247f9ae4976ade219f69dc8eaf1bf1ac6209149d429c257e7bccbe11f47eca3fc1363fb9')

get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-$(get_pyver)" python setup.py test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-av/LICENSE"
}
# vim:set ts=2 sw=2 et:

