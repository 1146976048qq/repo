# Maintainer: Frederick Zhang <frederick888@tsundere.moe>
pkgname=aribb25-git
_pkgname=aribb25
pkgver=0.2.7.r3.gc149386
pkgrel=1
pkgdesc="Basic implementation of the ARIB STD-B25 public standard"
arch=('x86_64')
url="https://code.videolan.org/videolan/aribb25"
license=('custom:ISC')
source=("git+https://code.videolan.org/videolan/aribb25.git")
sha256sums=('SKIP')
depends=('pcsclite')
makedepends=('git' 'libtool' 'autoconf' 'pkg-config' 'automake')
provides=('aribb25')
conflicts=('aribb25')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$_pkgname"
    ./bootstrap
    ./configure --prefix=/usr
    make
}

package() {
    cd "$_pkgname"
    make DESTDIR="$pkgdir/" install
    install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
