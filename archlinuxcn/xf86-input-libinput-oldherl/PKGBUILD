# Maintainer: Laurent Carlier <lordheavym@gmail.com>

_pkgname=xf86-input-libinput
pkgname=${_pkgname}-oldherl
pkgver=0.30.0
pkgrel=1.1
pkgdesc="Generic input driver for the X.Org server based on libinput"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libinput>=1.2.0')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.1' 'libxi' 'libx11' 'xorgproto')
conflicts=('xorg-server<1.19.0' 'X-ABI-XINPUT_VERSION<24' 'X-ABI-XINPUT_VERSION>=25' ${_pkgname})
provides=(${_pkgname})
source=(https://xorg.freedesktop.org/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2 0001-scroll-scale.patch)
sha512sums=('823f80f7d53866f2f06c1fb7b2d64111bf807329acaaeda4a09413cdb5f3e3235c1dd4f99183aae75adf5a01503e40e35b4449b31f977afd842b4723b062c380'
            'SKIP')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ../0001-scroll-scale.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
