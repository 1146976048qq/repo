# Maintainer: Plague-doctor <plague <at>> privacyrequired <<dot>> com >

_pkgname=vscodium
pkgname=$_pkgname-bin
pkgver=1.36.0
pkgrel=1
pkgdesc="Binary releases of VS Code without MS branding/telemetry/licensing."
arch=('x86_64')
url="https://github.com/VSCodium/vscodium"
license=('MIT')
depends=(
        fontconfig libxtst gtk3 python cairo alsa-lib nss gcc-libs libnotify libxss gconf
        'glibc>=2.28-4'
        )
optdepends=(
        'gvfs: For move to trash functionality'
        'libdbusmenu-glib: For KDE global menu'
)
provides=('code')
conflicts=('visual-studio-code-bin' 'code')
replaces=($_pkgname)
source=(
        vscodium.desktop
        ${_pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/releases/download/${pkgver}/VSCodium-linux-x64-${pkgver}.tar.gz
       )


sha256sums=('21643c97f15896b8789a20ae5fe5977377703830d8242613e4aab1a9144f8c45'
            '075fd7bb4419ec9ee72a7afb7b1ccde05562ed2dc82fe7790fc981af0ec27527')


package() {
    install -d -m755 ${pkgdir}/usr/bin
    install -d -m755 ${pkgdir}/usr/share/{vscodium,applications,pixmaps}
    # copy over all extracted files
    cp -Pr ${srcdir}/* ${pkgdir}/usr/share/vscodium
    # remove extra source symlinks
    find ${pkgdir}/usr/share/vscodium/ -type l -delete
    ln -s /usr/share/${_pkgname}/codium ${pkgdir}/usr/bin/${_pkgname}
    install -D -m644 vscodium.desktop ${pkgdir}/usr/share/applications/vscodium.desktop
    install -D -m644 ${srcdir}/resources/app/resources/linux/code.png \
            ${pkgdir}/usr/share/pixmaps/vscodium.png
}
