# Contributor: Alex "grevus" Lobtsov <alex@lobtsov.com>
# Contributor: Kai Korla <balticer@balticer.de>
pkgname=php-redis
_extname=phpredis
pkgver=4.3.0rc2
pkgrel=1
pkgdesc="PHP extension for interfacing with Redis"
url="http://pecl.php.net/package/redis"
arch=('x86_64')
license=('PHP')
makedepends=(
   'php-igbinary'
   'git'
)
depends=(
    'php-igbinary'
)
conflicts=('phpredis-git')
provides=('php-redis')
backup=('etc/php/conf.d/redis.ini')

source=(
    "https://github.com/phpredis/${_extname}/archive/${pkgver^^}.tar.gz"
    "git://github.com/nemequ/liblzf.git"
)

sha256sums=('6c1fdfd9d32552186df30045abd4c05b682464fe7449ad6ec03ff62955dd781c'
            'SKIP')
install=php-redis.install

prepare() {
  cd "$srcdir/$_extname-${pkgver^^}"

  cp -rf "$srcdir/liblzf" .
}

build() {
  cd "$srcdir/$_extname-${pkgver^^}"

  phpize
  ./configure --prefix=/usr --enable-redis-igbinary --enable-redis-lzf
  make
}

package() {
  cd "$srcdir/$_extname-${pkgver^^}"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=redis.so' > redis.ini
  install -Dm644 redis.ini "$pkgdir/etc/php/conf.d/redis.ini"
}
