# Maintainer: Butui Hu <hot123tea123@gmail.com>

_pkgname=ADIOS2
pkgname=adios2
pkgver=2.5.0
pkgrel=2
pkgdesc='Next generation of ADIOS developed in the Exascale Computing Program'
license=('Apache')
arch=('x86_64')
url='https://github.com/ornladios/ADIOS2'
depends=(
  'blosc'
  'libfabric'
  'libpng'
  'openmpi'
  'sz'
  'zeromq'
  'zfp'
)
makedepends=(
  'cmake'
  'gcc-fortran'
  'python-mpi4py'
  'python-numpy'
)
optdepends=(
  'python-numpy: python binding'
  'python-mpi4py: python binding'  
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ornladios/ADIOS2/archive/v${pkgver}.tar.gz")
sha256sums=('7c8ff3bf5441dd662806df9650c56a669359cb0185ea232ecb3578de7b065329')

build() {
  mkdir build
  cd build
  cmake "${srcdir}/${_pkgname}-${pkgver}" \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_SKIP_INSTALL_RPATH:BOOL=ON \
  -DADIOS2_USE_DataMan:BOOL=OFF \
  -DSZ_ROOT=/usr/include/sz
  make
}

check() {
  cd "build"
  ctest -v
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:

