# Maintainer: Butui Hu <hot123tea123@gmail.com>

_name=SimpleITK
pkgname=python-simpleitk
pkgver=2.0.0
pkgrel=3
pkgdesc="A layer built on top of the Insight Toolkit (ITK), intended to simplify and facilitate ITK's use in rapid prototyping, education and interpreted languages. This is the python binding."
arch=('x86_64')
url='http://www.simpleitk.org'
license=('Apache')
depends=(python-numpy)
makedepends=(
  clang
  doxygen
  git
  python-scikit-build
  python-setuptools
)
source=("${pkgname}-${pkgver}::git+https://github.com/SimpleITK/SimpleITKPythonPackage.git"
  git+https://github.com/SimpleITK/SimpleITK.git#tag=v${pkgver})
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  git submodule init
  git config submodule.${_name}.url "${srcdir}/${_name}"
  git submodule update
}

build() {
  export CC=clang
  export CXX=clang++
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
# vim:set ts=2 sw=2 et:

