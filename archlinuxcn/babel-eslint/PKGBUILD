pkgname=babel-eslint
pkgver=10.0.3
pkgrel=1
pkgdesc='allows you to lint ALL valid Babel code with the fantastic ESLint'
arch=('any')
url='https://github.com/babel/babel-eslint'
license=('MIT')
depends=('eslint')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('cf753b78c63aaffddfdff241f664ec2e3cb1aefd186f5cdbf0f09608ba60bb17c2cc1219530cb6dd1d6dfd729ec0ffbc9843760a4f03b6be2adb4cfa71ee92a0')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/$pkgname/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
