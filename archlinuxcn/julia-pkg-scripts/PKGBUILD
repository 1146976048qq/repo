# Maintainer:  <yuyichao@yyc2.yyc-arch.org>
pkgname=julia-pkg-scripts
pkgver=0.1.0.4.gbeae21e
pkgrel=3
pkgdesc="Packaging script for julia"
arch=('any')
license=('custom:MIT')
source=(git://github.com/yuyichao/julia-pkg-scripts
        julia-precompile.hook)
md5sums=('SKIP'
         '2cccd4cb8354783e32ef561487941bc2')
depends=(julia)
makedepends=(git)
install="julia-pkg.install"

pkgver() {
  cd julia-pkg-scripts

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

package() {
  depends=('julia')
  cd julia-pkg-scripts

  stdlib=$(julia --startup-file=no -e "print(lstrip(Sys.STDLIB, '/'))")

  mkdir -p "${pkgdir}/usr/lib/julia/"
  install -Dm644 julia-install-pkg.sh "${pkgdir}/usr/lib/julia/"
  install -Dm644 julia-compile-pkgs.jl "${pkgdir}/usr/lib/julia/"
  sed "s|@STDLIB@|$stdlib|" ../julia-precompile.hook |
    install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/julia-precompile.hook"
}
