# Maintainer: Ben Yip <yebenmy@protonmail.com>

_pkgname=PyMuPDF
pkgname=python-pymupdf
pkgver=1.14.8
pkgrel=1
pkgdesc="Python bindings for the PDF rendering library MuPDF"
arch=('any')
url="https://github.com/rk700/PyMuPDF"
license=('GPLv3+')
depends=('python' 'libmupdf' 'jbig2dec'  'openjpeg2'  'libjpeg' 'openssl' 'freetype2')
_name=${pkgname#python-}
source=(
"https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
'pymupdf-arch.patch'
)
md5sums=('3e48407ac5663f144b3b619c31d06545'
         '8d10cb0a11f293301d176f4fc630cd4d')


prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np2 -i "${srcdir}/pymupdf-arch.patch"
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  LANG=en_US.UTF-8 python3 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  LANG=en_US.UTF-8 python3 setup.py install --root=$pkgdir --optimize=1 --skip-build
}

# vim:set sw=2 et:
