# Maintainer: axionl <axionl@aosc.io>
pkgname=baidupcs-go-git
pkgver=3.6.2.r3.ga829eef
pkgrel=2
pkgdesc="The terminal utility for Baidu Network Disk (Golang Version)."
arch=('x86_64')
depends=('glibc')
makedepends=('git' 'go')
conflicts=("baidupcs")
provides=("baidupcs")
url="https://github.com/felixonmars/BaiduPCS-Go"
license=("Apache")

source=("${pkgname}::git+https://github.com/felixonmars/BaiduPCS-Go.git")
sha256sums=('SKIP')

pkgver() {
    cd "${pkgname}"

    ( set -o pipefail
      git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "${pkgname}"
    export GOPROXY="https://goproxy.io"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    go build -x -v -ldflags "-X main.Version=${pkgver} -s -w"
}

package() {
    cd "${pkgname}"
    install -Dm755 BaiduPCS-Go "${pkgdir}/usr/bin/baidupcs"
    install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
