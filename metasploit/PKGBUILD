# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Sabart Otto - Seberm <seberm[at]seberm[dot]com>
# Contributor: Tobias Veit - nIcE <m.on.key.tobi[at]gmail[dot]com>

pkgname=metasploit
pkgver=4.11.0
pkgrel=1
pkgdesc="An advanced open-source platform for developing, testing, and using exploit code"
url="https://www.metasploit.com/"
arch=('any')
license=('BSD')
depends=('ruby1.9' 'libpcap' 'postgresql-libs' 'ruby1.9-bundler')
optdepends=(
  'java-runtime: msfgui support'
  'ruby-pg: database support'
)
options=('!strip')
install="${pkgname}.install"
source=(https://github.com/rapid7/metasploit-framework/archive/${pkgver}.tar.gz)
sha512sums=('9462c2727a26fdfa5e626bb8bbf49229df7c736303d887c15f4dc6870d731fe4e6431a1bc2855b5f42f2f8bee5283d8559169bd2b7e8f18b86c0ff4d07f31889')

package() {
  cd metasploit-framework-${pkgver}

  mkdir -p "${pkgdir}/opt" "${pkgdir}/usr/bin"
  cp -r . "${pkgdir}/opt/${pkgname}"

  for f in ${pkgdir}/opt/${pkgname}/msf*; do
    local _msffile="${pkgdir}/usr/bin/`basename "${f}"`"
    echo "BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle-1.9 exec ruby-1.9 /opt/${pkgname}/`basename "${f}"` \"\$@\"" > ${_msffile}
    chmod 755 ${_msffile}
  done
  
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ts=2 sw=2 et:
