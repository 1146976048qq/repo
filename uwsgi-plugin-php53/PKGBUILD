# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Valentin HÄƒloiu <vially.ichb+aur@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>  
# Contributor: Kevin Zuber <uKev@knet.eu>
# Contributor: Vsevolod Balashov <vsevolod@balashov.name>

pkgname=uwsgi-plugin-php53
pkgdesc="Plugin for PHP 5.3 support"
pkgver=2.0.7
pkgrel=2
arch=(i686 x86_64)
url="http://projects.unbit.it/uwsgi"
license=(GPL2)
depends=('php53-embed' 'libyaml' 'jansson')
makedepends=('python2')
source=(http://projects.unbit.it/downloads/uwsgi-$pkgver.tar.gz
        archlinux.ini
        uwsgi_fix_rpath.patch
        uwsgi_trick_chroot.patch)

md5sums=('c18da6536f2f47a204814225ba695042'
         'e4d8e243b2faff21ec1353e9b1408103'
         '1a4516d5cdcf5b95b036f4eae2d0c152'
         '0c09a52fdb88f08c36a8b380f451ce6d')

prepare(){
    cd $srcdir/uwsgi-$pkgver
    cp $srcdir/archlinux.ini buildconf/archlinux.ini
    #sed -i 's/LIBS .*-lphp5.*/LIBS = []/' plugins/php/uwsgiplugin.py
    for patch in uwsgi_fix_rpath.patch uwsgi_trick_chroot.patch; do
        patch -Np1 -i $srcdir/$patch
    done
}

build() {
  pushd $srcdir/uwsgi-$pkgver

  python2 uwsgiconfig.py --plugin plugins/php archlinux php53
}

package(){
    install -dm755 $pkgdir/usr/bin
    install -Dm755 uwsgi-$pkgver/php53_plugin.so $pkgdir/usr/lib/uwsgi/php53_plugin.so
    ln -s uwsgi $pkgdir/usr/bin/uwsgi_${pkgname#uwsgi-plugin-}
}
